# hw3

## hw3

```verilog
`include "./I2C/usage_I2C_write_adjustable.v"
	module hw3(rst_in_n, clk, sfr_wr, sfr_rd, sfr_addr, sfr_data_out, sfr_data_in, i2c_sda, i2c_scl);
	/*---------parameter---------*/
	localparam IDLE   = 2'd0;
	localparam first  = 2'd1;
	localparam second = 2'd2;
	localparam third  = 2'd3;
	/*---------ports declaration---------*/
	input            rst_in_n, clk, sfr_wr, sfr_rd;
	input       [7:0]sfr_addr, sfr_data_out;
	inout            i2c_sda;
	output reg  [7:0]sfr_data_in;
	output wire      i2c_scl;
	/*---------variables---------*/
	reg     [1:0]fstate;
	reg          start, regldnACK;
	reg     [7:0]regctrl, regAddrH, regArrdL, regdata;
	wire    [8:0]ldnACK;
	/*---------I2C module instantiation---------*/
	usage_I2C_write_adjustable U0(
		.fstate(fstate),
		.en(start), 
		.clk_sys(clk), 
		.rst_n(rst_in_n), 
		.ctrlByte(regctrl), 
		.addrH(regAddrH), 
		.addrL(regArrdL), 
		.data_i(regdata),
		.SCLK(i2c_scl), 
		.SDA(i2c_sda), 
		.ldnACK(ldnACK)
	);
	/*---------start signal---------*/
	always@(posedge clk or negedge rst_in_n)begin
		if(!rst_in_n)start <= 1'b0;
		else begin
			if((sfr_addr==8'h9A)&&(sfr_data_out[0])&&(sfr_data_out[4]))start <= 1'b1;
			else                                                       start <= 1'b0;
		end
	end
	/*---------data register---------*/
	always@(posedge clk or negedge rst_in_n)begin
		if(!rst_in_n)begin
			regctrl  <= 8'h00;
			regAddrH <= 8'h00;
			regArrdL <= 8'h00;
			regdata  <= 8'h00;
		end
		else begin
			if((sfr_addr==8'h9C)&&sfr_wr)begin
				regctrl  <= sfr_data_out;
				regAddrH <= sfr_data_out;
				regArrdL <= sfr_data_out;
				regdata  <= sfr_data_out;
			end
			else if(ldnACK[5])begin
				regctrl  <= 8'h00;
				regAddrH <= 8'h00;
				regArrdL <= 8'h00;
				regdata  <= 8'h00;
			end
			else begin
				regctrl  <= regctrl;
				regAddrH <= regAddrH;
				regArrdL <= regArrdL;
				regdata  <= regdata;
			end
		end
	end
	/*---------sfr_data_in---------*/
	always@(posedge clk or negedge rst_in_n)begin
		if(!rst_in_n)sfr_data_in <= 8'h00;
		else begin
			if(regldnACK&&((sfr_addr==8'h01)))begin
				sfr_data_in <= 8'h01;
			end
			else sfr_data_in <= 8'h00;
		end
	end
	/*---------state---------*/
	always@(posedge clk or negedge rst_in_n)begin
		if(!rst_in_n)fstate <= third;
		else begin
			case(fstate)
				IDLE:begin
					if(start)fstate <= first;
					else     fstate <= IDLE;
				end
				first:begin
					if(start)fstate <= second;
					else     fstate <= first;
				end
				second:begin
					if(start)fstate <= third;
					else     fstate <= second;
				end
				third:begin
					if(start)fstate <= IDLE;
					else     fstate <= third;
				end
				default:fstate <= third;
			endcase
		end
	end
	/*---------output---------*/
	always@(posedge clk or negedge rst_in_n)begin
		if(!rst_in_n)regldnACK <= ldnACK[8];
		else begin
			case(fstate)
				IDLE:  regldnACK <= ldnACK[8];
				first: regldnACK <= ldnACK[7];
				second:regldnACK <= ldnACK[6];
				third: regldnACK <= ldnACK[5];
				default:regldnACK <= ldnACK[8];
			endcase
		end
	end
	endmodule
```

## usage_I2C_write_adjustable

```verilog
`include "./I2C/I2C_write_adjustable.v"
	module usage_I2C_write_adjustable(fstate ,en, clk_sys, rst_n, ctrlByte, addrH, addrL, data_i, SCLK, SDA, ldnACK, datastate);
	/*------ports declaration------*/
	input            clk_sys, rst_n, en;
	input       [1:0]fstate;
	input       [7:0]ctrlByte, addrH, addrL, data_i;
	inout            SDA;
	output           SCLK;
	output wire [1:0]datastate;
 	output wire [8:0]ldnACK;
	/*------variables------*/
	wire             rstACK, tick_I2C_neg;
	wire             totalACK;
	wire        [8:0]ACK;
	/*------instantiate I2C_write_adjustable------*/
	defparam I2C.WSize = 7'd34;
	defparam I2C.RSize = 7'd25;
	/*------assign wire------*/
	assign datastate = fstate;
	/*---------determined data size---------*/
	/////////////////////////////////////////
	//		7'd16--> 1 address + 1 data      //
	//		7'd25--> 1 address + 2 data      //
	//		7'd34--> 1 address + 3 data      //
	//		7'd43--> 1 address + 4 data      //
	//		7'd52--> 1 address + 5 data      //
	//		7'd61--> 1 address + 6 data      //
	//		7'd70--> 1 address + 7 data      //
	//		7'd79--> 1 address + 8 data      //
	/////////////////////////////////////////
	I2C_write_adjustable I2C(
		.clk_sys(clk_sys),
		.rst_n(rst_n),
		.en(en),
		.datastate(datastate),
		.data_W({ctrlByte, 1'b0, addrH, 1'b0, addrL, 1'b0, data_i, 1'b0}),
		.data_R({8'hff, 1'b0, 8'hff, 1'b0, 8'hff, 1'b0}),
		.totalACK(totalACK),
		.ACK(ACK), 
		.rstACK(rstACK),
		.SCLK(SCLK),
		.SDA(SDA), 
		.ldnACK(ldnACK), 
		.R_W(1'b0),
		.tick_I2C_neg(tick_I2C_neg)
	);
	endmodule
```

## I2C_write_adjustable

```verilog
`include "./I2C/I2C_control_adjustable.v"
	module I2C_write_adjustable#(parameter WSize = 7'd34, parameter RSize = 7'd25)(
	clk_sys, rst_n, en, datastate, data_W, data_R, totalACK, ACK, rstACK, SCLK, SDA, ldnACK, R_W, tick_I2C_neg);
	/*---------determined data size---------*/
	/////////////////////////////////////////
	//		7'd16--> 1 address + 1 data      //
	//		7'd25--> 1 address + 2 data      //
	//		7'd34--> 1 address + 3 data      //
	//		7'd43--> 1 address + 4 data      //
	//		7'd52--> 1 address + 5 data      //
	//		7'd61--> 1 address + 6 data      //
	//		7'd70--> 1 address + 7 data      //
	//		7'd79--> 1 address + 8 data      //
	/////////////////////////////////////////
	/*---------ports declaration---------*/
	input                  clk_sys, rst_n, en, R_W;
	input       [WSize+1:0]data_W;//read
	input       [RSize+1:0]data_R;//write
	input             [1:0]datastate;
	output reg        [8:0]ACK;
	output wire       [8:0]ldnACK;
	output reg             tick_I2C_neg;
	output wire            totalACK, rstACK, SCLK;
	inout                  SDA;
	/*---------variables---------*/
	reg       [6:0]count;
	reg       [9:0]cnt_I2C;
	reg [WSize+1:0]regdata_W;
	reg [RSize+1:0]regdata_R;
	reg            tick_I2C;
	reg            SCLK_50k;
	wire           SCLK_temp, SHEN, LDEN, SDO, countEN, rstcount;
	wire           SEL;
	/*---------assign wire---------*/
	assign SEL = (SHEN)?((!R_W)?(regdata_W[WSize+1]):(regdata_R[RSize+1])):(SDO);
	assign SDA = (SEL)?(1'bz):(1'b0);
	assign totalACK = |ACK;
	/*---------module instantiate---------*/
	defparam I2C.WSize = WSize;
	defparam I2C.RSize = RSize;
	I2C_control_adjustable I2C(
		.clk_sys(clk_sys),
		.SCLK_50k(SCLK_50k),
		.tick_I2C(tick_I2C),
		.rst_n(rst_n),
		.en(en),
		.count(count),
		.countEN(countEN),
		.rstcount(rstcount),
		.ldnACK(ldnACK),
		.rstACK(rstACK),
		.SCLK(SCLK),
		.SCLK_temp(SCLK_temp),
		.SHEN(SHEN),
		.LDEN(LDEN),
		.SDO(SDO),
		.R_W(R_W)
	);
	/*---------100k counter---------*/
	always@(posedge clk_sys or negedge rst_n)begin
		if(!rst_n)cnt_I2C <= 10'd0;
		else begin
			if(cnt_I2C<10'd999)cnt_I2C <= cnt_I2C + 10'd1;//0-99
			else               cnt_I2C <= 10'd0;
		end
	end
	/*---------I2C tick---------*/
	always@(posedge clk_sys or negedge rst_n)begin
		if(!rst_n)tick_I2C <= 1'b0;
		else begin
			if(cnt_I2C==10'd998)tick_I2C <= 1'b1;
			else                tick_I2C <= 1'b0;
		end
	end
	/*---------I2C tick_neg---------*/
	always@(posedge clk_sys or negedge rst_n)begin
		if(!rst_n)tick_I2C_neg <= 1'b0;
		else begin
			if(cnt_I2C==10'd498)tick_I2C_neg <= 1'b1;
			else                tick_I2C_neg <= 1'b0;
		end
	end
	/*---------SCLK_50k---------*/
	always@(posedge clk_sys or negedge rst_n)begin
		if(!rst_n)SCLK_50k <= 1'b0;
		else begin
			if(cnt_I2C==10'd400)     SCLK_50k <= 1'b0;//499
			else if(cnt_I2C==10'd600)SCLK_50k <= 1'b1;//999
			else                     SCLK_50k <= SCLK_50k;
		end
	end
	/*---------counter---------*/
	always@(posedge clk_sys or negedge rst_n)begin
		if(!rst_n)begin
			count <= 7'd0;
		end
		else begin
			if(tick_I2C)begin
				if(rstcount)    count <= 7'd0;
				else if(countEN)count <= count + 7'd1;
				else            count <= count;
			end
			else count <= count;
		end
	end
	/*---------data---------*/
	always@(posedge clk_sys or negedge rst_n)begin
		if(!rst_n)regdata_W <= 'd0;
		else begin
			if(!R_W&&tick_I2C)begin
				if(LDEN)                    regdata_W <= data_W;
				else if(SHEN)               regdata_W <= {regdata_W[WSize:0],1'b0};//left rotate
				else                        regdata_W <= regdata_W;
			end
			else if(en&&(datastate==2'd00))regdata_W <= {1'b0,data_W[26:0],8'd0};
			else if(en&&(datastate==2'd01))regdata_W <= {1'b0,data_W[17:0],17'd0};
			else if(en&&(datastate==2'd02))regdata_W <= {1'b0,data_W[8:0] ,26'd0};
			else regdata_W <= regdata_W;
		end
	end
	always@(posedge clk_sys or negedge rst_n)begin
		if(!rst_n)regdata_R <= 'd0;
		else begin
			if(R_W&&tick_I2C)begin
				if(LDEN)     regdata_R <= data_R;
				else if(SHEN)regdata_R <= {regdata_R[RSize:0],1'b0};//left rotate
				else         regdata_R <= regdata_R;
			end 
			else regdata_R <= regdata_R;
		end
	end
	/*---------ACK---------*/
	always@(posedge clk_sys or negedge rst_n)begin
		if(!rst_n)ACK  <= 9'd0;
		else if(rstACK&&tick_I2C)ACK  <= 9'd0;
		else begin
			if(ldnACK[8]&&tick_I2C)ACK[8] <= SDA;
			else                   ACK[8] <= ACK[8];
			if(ldnACK[7]&&tick_I2C)ACK[7] <= SDA;
			else                   ACK[7] <= ACK[7];
			if(ldnACK[6]&&tick_I2C)ACK[6] <= SDA;
			else                   ACK[6] <= ACK[6];
			if(ldnACK[5]&&tick_I2C)ACK[5] <= SDA;
			else                   ACK[5] <= ACK[5];
			if(ldnACK[4]&&tick_I2C)ACK[4] <= SDA;
			else                   ACK[4] <= ACK[4];
			if(ldnACK[3]&&tick_I2C)ACK[3] <= SDA;
			else                   ACK[3] <= ACK[3];
			if(ldnACK[2]&&tick_I2C)ACK[2] <= SDA;
			else                   ACK[2] <= ACK[2];
			if(ldnACK[1]&&tick_I2C)ACK[1] <= SDA;
			else                   ACK[1] <= ACK[1];
			if(ldnACK[0]&&tick_I2C)ACK[0] <= SDA;
			else                   ACK[0] <= ACK[0];
		end
	end
	endmodule
```

## I2C_control_adjustable

```verilog
module I2C_control_adjustable#(parameter WSize=7'd34, parameter RSize=7'd25)(
	clk_sys, SCLK_100k, tick_I2C, rst_n, en, count, countEN, rstcount, ldnACK, 
	rstACK, SCLK, SCLK_temp,  SHEN, LDEN, SDO, R_W
	);
	/*---------determined data size---------*/
	/////////////////////////////////////////
	//		7'd16--> 1 address + 1 data      //
	//		7'd25--> 1 address + 2 data      //
	//		7'd34--> 1 address + 3 data      //
	//		7'd43--> 1 address + 4 data      //
	//		7'd52--> 1 address + 5 data      //
	//		7'd61--> 1 address + 6 data      //
	//		7'd70--> 1 address + 7 data      //
	//		7'd79--> 1 address + 8 data      //
	/////////////////////////////////////////
	/*---------parameter---------*/
	localparam IDLE  = 3'd0;
	localparam GO    = 3'd1;
	localparam START = 3'd2;
	localparam WAIT  = 3'd3;
	localparam SHIFT = 3'd4;
	localparam STOP  = 3'd5;
	localparam FINAL = 3'd6;
	localparam END   = 3'd7;
	/*---------ports declaration---------*/
	input       clk_sys, SCLK_100k, tick_I2C, rst_n, en, R_W;
	input       [6:0]count;
	output reg  SCLK_temp, SHEN, LDEN, SDO, countEN, rstcount, rstACK;
	output reg  [8:0]ldnACK;
	output wire SCLK;
	/*---------variables---------*/
	reg  [2:0]fstate;
	wire [6:0]size;
	/*---------assign wire---------*/
	assign SCLK = (SHEN)?(~SCLK_100k):(SCLK_temp);
	assign size = (R_W)?(RSize):(WSize);
	/*---------fstate state---------*/
	always@(posedge clk_sys or negedge rst_n)begin
		if(!rst_n)begin
			fstate <= IDLE;
		end
		else begin
			case(fstate)
				IDLE:begin
					if(en) fstate <= GO;
					else   fstate <= IDLE;
				end
				GO:begin
					if(tick_I2C)fstate <= START;
					else        fstate <= GO;
				end
				START:begin
					if(tick_I2C)fstate <= WAIT;
					else        fstate <= START;
				end
				WAIT:begin
					if(tick_I2C)fstate <= SHIFT;
					else        fstate <= WAIT;
				end
				SHIFT:begin
					if(count==size&&tick_I2C)fstate <= STOP;
					else                     fstate <= SHIFT;
				end
				STOP:begin
					if(tick_I2C)fstate <= FINAL;
					else        fstate <= STOP;
				end
				FINAL:begin
					if(tick_I2C)fstate <= END;
					else        fstate <= FINAL;
				end
				END:begin
					if(tick_I2C)fstate <= IDLE;
					else        fstate <= END;
				end
			endcase
		end
	end
	/*---------fstate output---------*/
	always@(posedge clk_sys or negedge rst_n)begin
		if(!rst_n)begin
			SCLK_temp <= 1'b1;
			LDEN      <= 1'b0;
			SHEN      <= 1'b0;
			ldnACK    <= 9'd0;
			SDO       <= 1'b1;//SDIN_temp control data/raising/falling
			countEN   <= 1'b0;
			rstcount  <= 1'b0;
			rstACK    <= 1'b0;
		end
		else begin
			if(tick_I2C)begin
				case(fstate)
					IDLE:begin
						SCLK_temp <= 1'b1;//high
						LDEN      <= 1'b0;
						SHEN      <= 1'b0;
						ldnACK    <= 9'd0;
						SDO       <= 1'b1;//high
						countEN   <= 1'b0;
						rstcount  <= 1'b0;
						rstACK    <= 1'b0;
					end
					GO:begin
						SCLK_temp <= 1'b1;//high
						LDEN      <= 1'b1;//load data
						SHEN      <= 1'b0;
						ldnACK    <= 9'd0;
						SDO       <= 1'b1;//high
						countEN   <= 1'b0;
						rstcount  <= 1'b0;
						rstACK    <= 1'b0;
					end
					START:begin
						SCLK_temp <= 1'b1;//high
						LDEN      <= 1'b0;
						SHEN      <= 1'b0;
						ldnACK    <= 9'd0;
						SDO       <= 1'b0;//falling(start)
						countEN   <= 1'b0;
						rstcount  <= 1'b0;
						rstACK    <= 1'b0;
					end
					WAIT:begin
						SCLK_temp <= 1'b1;//high
						LDEN      <= 1'b0;
						SHEN      <= 1'b0;
						ldnACK    <= 9'd0;
						SDO       <= 1'b0;//low
						countEN   <= 1'b0;
						rstcount  <= 1'b0;
						rstACK    <= 1'b0;
					end
					SHIFT:begin
						SCLK_temp <= 1'b0;//don't care
						LDEN      <= 1'b0;
						SHEN      <= 1'b1;//shifting
						//ldnACK
						case(count)
							7'd7: ldnACK <= 9'd256;
							7'd16:ldnACK <= 9'd128;
							7'd25:ldnACK <= 9'd64;
							7'd34:ldnACK <= 9'd32;
							7'd43:ldnACK <= 9'd16;
							7'd52:ldnACK <= 9'd8;
							7'd61:ldnACK <= 9'd4;
							7'd70:ldnACK <= 9'd2;
							7'd79:ldnACK <= 9'd1;
							default:ldnACK <= 9'd0;
						endcase
						SDO       <= 1'b1;//don't care(data[26])
						countEN   <= 1'b1;//counting
						//rstcount
							if(count==size)rstcount  <= 1'b1;
							else           rstcount  <= 1'b0;
						rstACK    <= 1'b0;
					end
					STOP:begin
						SCLK_temp <= 1'b0;//stop the clock
						LDEN      <= 1'b0;
						SHEN      <= 1'b0;
						ldnACK    <= 9'd0;
						SDO       <= 1'b0;//low
						countEN   <= 1'b0;
						rstcount  <= 1'b0;
						rstACK    <= 1'b0;
					end
					FINAL:begin
						SCLK_temp <= 1'b1;//high
						LDEN      <= 1'b0;
						SHEN      <= 1'b0;
						ldnACK    <= 9'd0;
						SDO       <= 1'b0;//low
						countEN   <= 1'b0;
						rstcount  <= 1'b0;
						rstACK    <= 1'b0;
					end
					END:begin
						SCLK_temp <= 1'b1;//high
						LDEN      <= 1'b0;
						SHEN      <= 1'b0;
						ldnACK    <= 9'd0;
						SDO       <= 1'b1;//raising
						countEN   <= 1'b0;
						rstcount  <= 1'b0;
						rstACK    <= 1'b1;//reset ACK
					end
				endcase
			end
			else begin
				SCLK_temp <= SCLK_temp;
				LDEN      <= LDEN;
				SHEN      <= SHEN;
				ldnACK    <= ldnACK;
				SDO       <= SDO;
				countEN   <= countEN;
				rstcount  <= rstcount;
				rstACK    <= rstACK;
			end
		end
	end
	endmodule
```

## TestBench

```verilog
`timescale 1ns / 10ps
	/*
	`include "int_mem.v"
	`include "ext_mem.v"
	`include "rom_mem.v"
	`include "DW8051_core.v"
	`include "M24AA256.v"
	`include "sfr_mem.v"
	*/
	module tb_8051();
   parameter       clkx8 = 27.08;  // 48*64=3.072 MHZ,325/12=27.08

   reg clk; 
   reg por_n; 
   reg rst_in_n; 
   wire rst_out_n; 
   reg test_mode_n; 
   wire stop_mode_n; 
   wire idle_mode_n; 
   wire [7:0] sfr_addr; 
   wire [7:0] sfr_data_out; 
   wire [7:0] sfr_data_in; 
   wire sfr_wr; 
   wire sfr_rd; 
   wire [15:0] mem_addr; 
   wire [7:0] mem_data_out; 
   wire [7:0] mem_data_in; 
   wire mem_wr_r; 
   wire mem_rd_n; 
   wire mem_pswr_n;    
   
   wire mem_psrd_n; 
   wire mem_ale; 
   reg mem_ea_n; 
   wire int0_n; 
   wire int1_n; 
   wire int2; 
   wire int3_n; 
   wire int4; 
   wire int5_n; 
   wire pfi; 
   wire wdti; 
   wire rxd0_in; 
   wire rxd0_out, txd0; 
   wire rxd1_in; 
   wire rxd1_out, txd1; 
   wire t0; 
   wire t1; 
   wire t2; 
   wire t2ex; 
   wire t0_out, t1_out, t2_out; 
   wire port_pin_reg_n, p0_mem_reg_n, p0_addr_data_n, p2_mem_reg_n; 
   wire [7:0] iram_addr, iram_data_out, iram_data_in; 
   wire iram_rd_n, iram_we1_n, iram_we2_n; 
   wire [15:0] irom_addr; 
   wire [7:0] irom_data_out; 
   wire irom_rd_n, irom_cs_n; 
  
   wire [7:0] P0,P1,P2,P3;
   
   wire	i2c_sda;
   wire	i2c_scl;

   /*------------*/
	
	
	
   pullup(i2c_sda);
   pullup(i2c_scl);
   
   always
   begin
      #(clkx8/2) clk <= 1'b1 ;
      #(clkx8/2) clk <= 1'b0 ;
   end 

   initial
    begin
   
      //$fsdbDumpfile("tb_8051.fsdb");
      //$fsdbDumpfile("tb_8051_io.fsdb"); 
      //$fsdbDumpvars;
            
      mem_ea_n = 1'b1;              // 1: external ROM ,0: Internal ROM
      por_n = 1'b1 ;
      rst_in_n  = 1'b1;
      test_mode_n = 1'b1;
      #(clkx8*10);
      por_n = 1'b0 ;
      rst_in_n  = 1'b0;
      #(clkx8*10);
      por_n = 1'b1 ;
      rst_in_n  = 1'b1;
      
      wait((irom_addr == 16'h0B16) | (irom_addr == 16'h0B19));
      #1000;
      $stop;

    end
     
     
  
//--------------------------------------------------------------- 
// DW8051 instantiation: 
//--------------------------------------------------------------- 
DW8051_core u0 ( 
               .clk (clk), 
               .por_n (por_n), 
               .rst_in_n (rst_in_n), 
               .rst_out_n (rst_out_n), 
               .test_mode_n (test_mode_n), 
               .stop_mode_n (stop_mode_n), 
               .idle_mode_n (idle_mode_n), 
               .sfr_addr (sfr_addr), 
               .sfr_data_out (sfr_data_out), 
               .sfr_data_in (sfr_data_in), 
               .sfr_wr (sfr_wr), 
               .sfr_rd (sfr_rd), 
               .mem_addr (mem_addr), 
               .mem_data_out (mem_data_out), 
               .mem_data_in (mem_data_in), 
               .mem_wr_n (mem_wr_n), 
               .mem_rd_n (mem_rd_n), 
               .mem_pswr_n (mem_pswr_n), 
               .mem_psrd_n (mem_psrd_n), 
               .mem_ale (mem_ale), 
               .mem_ea_n (mem_ea_n),
               .int0_n (int0_n), 
               .int1_n (int1_n), 
               .int2 (int2), 
               .int3_n (int3_n), 
               .int4 (int4), 
               .int5_n (int5_n), 
               .pfi (pfi), 
               .wdti (wdti), 
               .rxd0_in (rxd0_in), 
               .rxd0_out (rxd0_out), 
               .txd0 (txd0), 
               .rxd1_in (rxd1_in), 
               .rxd1_out (rxd1_out), 
               .txd1 (txd1), 
               .t0 (t0), 
               .t1 (t1), 
               .t2 (t2), 
               .t2ex (t2ex), 
               .t0_out (t0_out), 
               .t1_out (t1_out), 
               .t2_out (t2_out), 
               .port_pin_reg_n (port_pin_reg_n), 
               .p0_mem_reg_n (p0_mem_reg_n), 
               .p0_addr_data_n (p0_addr_data_n), 
               .p2_mem_reg_n (p2_mem_reg_n), 
               .iram_addr (iram_addr), 
               .iram_data_out (iram_data_out), 
               .iram_data_in (iram_data_in), 
               .iram_rd_n (), 
               .iram_we1_n (iram_we1_n), 
               .iram_we2_n (iram_we2_n), 
               .irom_addr (irom_addr), 
               .irom_data_out (irom_data_out), 
               .irom_rd_n (irom_rd_n), 
               .irom_cs_n (irom_cs_n) 
               );
    /*                 
   sfr_mem      u1_sfr_mem(
                        .clk(clk),
                        .addr(sfr_addr),     
                        .data_in(sfr_data_out),
                        .data_out(sfr_data_in),
                        .wr_n(~sfr_wr),       
                        .rd_n(~sfr_rd)) ;  
        
    */               
   int_mem      u3_int_mem(
                        .clk(clk),
                        .addr(iram_addr),
                        .data_in(iram_data_in),
                        .data_out(iram_data_out),
                        .we1_n(iram_we1_n),
                        .we2_n(iram_we2_n),
                        .rd_n(iram_rd_n));  
                        
                                        
   ext_mem      u2_ext_mem(
                        .addr(mem_addr),
                        .data_in(mem_data_out),
                        .data_out(mem_data_in),
                        .wr_n(mem_wr_n),
                        .rd_n(mem_rd_n));    
                        
                          
  rom_mem       u4_rom_mem(
                        .addr(irom_addr),
                        .data_out(irom_data_out),                    
                        .rd_n(irom_rd_n),
                        .cs_n(irom_cs_n));  
//--------------------------------------------------------------- 
// EEPROM Model
//---------------------------------------------------------------                         
 M24AA256 u10(
    .A0(1'b0), 
    .A1(1'b0), 
    .A2(1'b0), 
    .WP(1'b0), 
    .SDA(i2c_sda), 
    .SCL(i2c_scl), 
    .RESET(~rst_in_n)
    );   
                                                         
hw3 u11(
    .rst_in_n(rst_in_n),
    .clk(clk),
    .sfr_wr(sfr_wr),
    .sfr_rd(sfr_rd),
    .sfr_addr(sfr_addr),
    .sfr_data_out(sfr_data_out),
    .sfr_data_in(sfr_data_in),
    .i2c_sda(i2c_sda),
    .i2c_scl(i2c_scl)
);                      
 
initial
  begin
  $monitor("time=%3d memory_000=0x%x memory_001=0x%x",$time,u10.MemoryByte_000[7:0],u10.MemoryByte_001[7:0]);
  end
   
endmodule
```

## Wave

![hw3%209f01092b49314c8986d43941b970e16b/Untitled.png](hw3%209f01092b49314c8986d43941b970e16b/Untitled.png)

![hw3%209f01092b49314c8986d43941b970e16b/Untitled%201.png](hw3%209f01092b49314c8986d43941b970e16b/Untitled%201.png)

![hw3%209f01092b49314c8986d43941b970e16b/Untitled%202.png](hw3%209f01092b49314c8986d43941b970e16b/Untitled%202.png)

## Monitor

![hw3%209f01092b49314c8986d43941b970e16b/Untitled%203.png](hw3%209f01092b49314c8986d43941b970e16b/Untitled%203.png)

## Compilation Report

![hw3%209f01092b49314c8986d43941b970e16b/Untitled%204.png](hw3%209f01092b49314c8986d43941b970e16b/Untitled%204.png)

## RTL Viewer

![hw3%209f01092b49314c8986d43941b970e16b/Untitled%205.png](hw3%209f01092b49314c8986d43941b970e16b/Untitled%205.png)

![hw3%209f01092b49314c8986d43941b970e16b/Untitled%206.png](hw3%209f01092b49314c8986d43941b970e16b/Untitled%206.png)

![hw3%209f01092b49314c8986d43941b970e16b/Untitled%207.png](hw3%209f01092b49314c8986d43941b970e16b/Untitled%207.png)

![hw3%209f01092b49314c8986d43941b970e16b/Untitled%208.png](hw3%209f01092b49314c8986d43941b970e16b/Untitled%208.png)

## State Machine

![hw3%209f01092b49314c8986d43941b970e16b/Untitled%209.png](hw3%209f01092b49314c8986d43941b970e16b/Untitled%209.png)