# final

```verilog
# [Day1]概述
## Verilog是什麼？
Verilog是一種硬體描述語言(**Hardware Description Language, HDL**)，用於數位電路的系統設計，是一種描述數位電路的語言，設計者設計數位電路時可以透過這種語言來描述自己的電路設計想法，利用EDA Tool來幫你完成電路設計。
目前HDL分為兩大類，有Verilog及VHDL兩種，在歐洲國家以VHDL較為普遍，而亞洲國家則是以Verilog較為多人使用。

---

## 撰寫Verilog時應該注意的那些事
前面說過Verilog是硬體描述語言，描述硬體的語言，雖然語法與C相似，但概念卻不太相同，c語言是由上至下一行一行的執行，而verilog是每個always block都會同步執行的，因此在設計時就要特別注意，並且要以硬體的角度去寫，否則寫出來的程式可能不會那麼理想。

Verilog主要有四種層級的描述方法：
- Behavioral level:
  - Verilog HDL中的最高層，我們只需針對電路的功能來做設計，不需要考慮底層硬體架構。
- Dataflow level:
  - 這裡必須指明訊號處理的方法，在這裡會使用assign語法來對訊號做處理。
- Gate level:
  - 這裡是由邏輯閘所連接而成。
- Switch level:
  - 這裡是由電晶體元件所連接而成。(*現在幾乎沒什麼在用了*)

`不過通常寫Verilog 時只會用到 Behavioral level 以及 Dataflow level。`

# [Day2] Verilog 基本簡介
## verilog電路基本架構
舉個簡單電路的例子：
```
module adder(
  a, 
  b, 
  c
);

input a;  //輸入埠 敘述
input b;  //輸入埠 敘述
output c; //輸出埠 敘述
wire c;  //資料型態 敘述
assign c = a & b;//內部電路 敘述

endmodule
```
上方為一個AND邏輯閘，由此可知，一個完整的模組是由`module`以及`endmodule`包起來的，而adder那個位置就要放的則是`模組名稱`，而後面的括號內則是放`所有的輸入及輸出腳位`，接著裡面的最上方會宣告每隻腳位是輸出還是輸入，在來是宣告變數的資料型態。
在這裡，只要是輸入，一律都是`input`，輸出的話，如果沒有特別宣告則默認`wire`。

- input
  - 模組內只可接 wire
  - 模組外可接   wire、reg

- Output
  - 模組內可接   wire、reg
  - 模組外只可接 wire

- InOut（雙向埠）
  - 模組內&外只可接 wire

---

## 那甚麼時候才會用到inout呢？
舉個例子，在實現I2C protocol時就會用到這個好用的東西了，為甚麼這麼說呢？先來看看下面這張圖
![](https://i.imgur.com/4Ls8rP4.jpg)
[圖片出處](https://china.cypress.com/documentation/application-notes/an50987-getting-started-i2c-psoc-1)
I2C的運作機制是這樣的，I2C僅使用兩個雙BUS，串列資料線（SDA）和串列時鐘線（SCL），因此當master送資料給slave時此時master的SDA要設成out狀態，而slave的SDA要設成in的狀態，相反過來，當slave送資料給master時此時slave的SDA要設成out狀態，而master的SDA要設成in的狀態，所以會需要可輸入又可輸出的inout資料型態。

# [Day3] Verilog 資料型態(上)
## 資料型態
|  值  |   意義    |
|:---:|:----------:|
| `0` | 低電位(邏輯0)
| `1` | 高電位(邏輯1)
| `Z` | 高阻抗( High Impendence ) 
| `X` | 未知的值(Unknow)or邏輯衝突)

---

## 連接線：wire、wand、wor
- 沒有記憶性
- 預設值為`z`
- 將兩個wire連在一起是不允許的
- 若是型態為wand/wor則例外
舉個例子：
```
module test(
  a, 
  b, 
  m, 
  n
);
input a;
input b;
output m;
output n;
wand m;
wor n;
    
// wire and ---> m = a&b
assign m = a;
assign m = b;

// wire or ---> n = a|b
assign n = a;
assign n = b;

endmodule
```

---

## 暫存器：reg
- 有記憶性
- 預設值為x (最好要初始化，通常使用rst或rst_n訊號觸發初始化)
舉個例子：
```
module test(
  clk, 
  rst_n, 
  a, 
  b
);
input clk;
input a;
input rst_n;
output b;
reg b;
    
always@(posedge clk or negedge rst_n)begin
  if(!rst_n)b <= 1'b0;
  else      b <= a;
end
    
endmodule
```

> 到這邊後，應該有些人對於reg與wire的使用不是很理解，先來解釋位甚麼第二個例子需要用reg型態，`因為變數b在alwaye內賦值，而always又是屬於觸發型電路，所以需要用暫存器儲存<前態>與<次態>，是不可用wire的(wire沒有記憶性)`，順帶一提，這邊的reset如果是負緣，那我們通常會加個_n，讓別人知道那是負緣觸發，若是正緣的話則不用加，這樣的coding-style是比較好的！！

# [Day4] Verilog 資料型態(下)
## 各種進制表示法
```
<位元長度> ’ <b、o、d、h> <數值>
```
- 位元長度：以十進制表示幾個bit數
- 進制表示：二進制(b)、八進制(o)、十進制(d)、十六進制(h)，不打則預設為十進制
- 數值資料：可用底線 _ 來增加可讀性
EX:
```
a = 32'd0;
b = 6'b000101;
c = 8'h0A;
d = 8'b0101_1010;
```
`錯誤用法舉例：`
```
a = 32'b0;//打了b就要把所有位打出來
b = 6'd000101;//數值使用錯誤
c = 8'h10A;位元長度錯誤
```

---

## 向量(陣列)表示法
EX:
```
reg [7:0]a;//一個8bit的reg
reg [3:0]b[31:0];//32個4bit的reg--->又稱記憶體表示法
```
**EX:用向量表示法寫一個有讀寫及致能的簡易memory**
```
module memory(
  Enable,
  ReadWrite,
  Address, 
  DataIn,
  DataOut
);
/*----------ports declarations----------*/
input        Enable;
input        ReadWrite;
input   [5:0]Address; 
input   [3:0]DataIn;
output  [3:0]DataOut;
reg     [3:0]DataOut;
/*----------variables----------*/
reg     [3:0]mem[63:0];
/*----------memory----------*/
always@(Enable or ReadWrite)begin
  if(Enable)begin
    if(ReadWrite)DataOut      = mem[Address];
    else         mem[Address] = DataIn;
  end
  else DataOut = 4'bzzzz;
end

endmodule
```
**TestBench**
```
`timescale 1ns/1ns
module tb_memory();
/*----------variables----------*/
reg      Enable;
reg      ReadWrite;
reg [5:0]Address; 
reg [3:0]DataIn;
wire[3:0]DataOut;
integer i = 0;
/*----------module memory instantiation----------*/
memory UUT(
  .Enable(Enable),
  .ReadWrite(ReadWrite),
  .Address(Address), 
  .DataIn(DataIn),
  .DataOut(DataOut)
);
/*----------control signal----------*/
initial begin
  Enable    = 1'b0;
  ReadWrite = 1'b0;
  Address   = 6'd0;
  DataIn    = 4'd0;
end

initial begin
  for(i=0;i<=63;i=i+1)begin
    #100;
    Enable    = 1'b1;
    ReadWrite = 1'b0;
    Address   = Address + 6'd1;
    DataIn    = DataIn  + 4'd1;
    #10;
    Enable    = 1'b0;
  end
  Address   = 6'd0;
  for(i=0;i<=63;i=i+1)begin
    #100;
    Enable    = 1'b1;
    ReadWrite = 1'b1;
    Address   = Address + 6'd1;
    DataIn    = 4'dz;
    #10;
    Enable    = 1'b0;
  end
#1000;
$stop;
end

always@(negedge Enable)begin
  if(ReadWrite)$display("time = %3d, dataRead  = %x", $time, DataOut);
end

endmodule
```

**Wave**
![](https://i.imgur.com/sgFNz36.png)
![](https://i.imgur.com/Hbn57P0.png)

`這邊可以看到前面是寫入值到各個位址，下圖則是成功從該位址讀出先前存入的值。`

(這邊ReadWrite是'0'寫入，'1'讀取)

有趣的是quartus竟然可以辨識那是一顆mem!
![](https://i.imgur.com/CSU5QJI.png)

---

在這裡可以順便提到integer的使用，*integer好比一個32bit的register*，所以不要輕易用它來宣告變數，否則會無形中多使用了很多硬體資源，它通常會被宣告來當for-loop的迴圈變數。
EX:
```
reg [3:0]a[31:0];

always@(posedge clk or negedge rst_n)begin
  if(!rst_n)begin
    for(i=0;i<32;i=i+1)begin
      a <= 4'd0;
    end
  end
  else begin
  .
  .
  .
  end
end
```

---

## 參數parameter
- 是一個宣告了就`無法更動的常數`
- 常常會用來指定`資料位寬(Width)`或是`狀態機的值`
EX:
```
parameter width = 32;
reg [width-1:0]a;//一個32bit的reg
```

# [Day5] Gate Level
## 一些基本邏輯閘
![](https://i.imgur.com/OOIKYkm.jpg)
[圖片出處](https://frankcomputerscience.wordpress.com/chapter-3/)

---

## 語法
```
<邏輯閘種類> <邏輯閘命名> (output, in1, in2);
```
- 邏輯閘種類：AND、OR、NOT....。
- 邏輯閘命名：賦予這個邏輯閘一個名子，`名子不可重複。`

EX:
**半加器：**
![](https://i.imgur.com/yqIuFAt.png)

```
//Half adder
module Half_adder(
  a, 
  b, 
  carry, 
  sum
);

input a;
input b;
output carry;
output sum;
and andl(carry, a, b);//AND gate carry = a and b
xor xor1(sum, a, b);//XOR gate sum = a xor b

endmodule
```
**全加器：**
![](https://i.imgur.com/VScAo8t.png)

```
//Full_adder
module Full_adder(
  x, 
  y, 
  c_in, 
  c_out, 
  s
);
input x;
input y;
input c_in;
output c_out;
output s;
wire sl;
wire cl;
wire c2;
Half_adder HAl(x, y, cl, sl);//instantiate Half_adder HAI
Half_adder HA2(c_in, sl, c2, s);//instantiate Half_adder HA2
or(c_out, cl, c2);//OR gate c_out = cl | c2

endmodule
```
**四位元加法器：**
![](https://i.imgur.com/z0oEZEM.png)
```
module Full_adder_FourBits(
  a_in, 
  b_in, 
  c_in, 
  carry_out, 
  sum_out
);
input  [3:0] a_in;
input  [3:0] b_in;
input        c_in;
output [3:0] sum_out;
output       carry_out;
wire   [3:0] sum_out; 
wire carry_out;
wire c1;
wire c2;
wire c3;

Full_adder FA1(a_in[0], b_in[0], c_in, sum_out[0], c1);//instantate FA FA1
Full_adder FA2(a_in[1], b_in[1], c1, sum_out[1], c2);//instantate FA FA2
Full_adder FA3(a_in[2], b_in[2], c2, sum_out[2], c3);//instantate FA FA3
Full_adder FA4(a_in[3], b_in[3], c3, sum_out[3], carry_out);//instantate FA FA4

endmodule

```

# [Day6] Dataflow Level
## 算術運算子
|  運算子  |   說明    |
|:---:|:----------:|
| `+` | 加法
| `-` | 減法
| `*` | 乘法
| `**` | 次方
| `/` | 除法
| `%` | 取餘數

`在這裡的 "/" 以及 "%" 撰寫時盡量少用，因為會非常耗硬體資源`

---

## 二元運算子
|  運算子  |   說明    |
|:---:|:----------:|
| `~` | NOT
| `&` | AND
| `/(直的)` | OR
| `^` | XOR

---

## 邏輯運算子
|  運算子  |   說明    |
|:---:|:----------:|
| `!` | 邏輯NOT
| `&&` | 邏輯AND
| `//(也是直的)` | 邏輯OR

---

## 比較用的運算子
|  運算子  |   說明    |
|:---:|:----------:|
| `>` | 大於
| `<` | 小於
| `>=` | 大於等於
| `<=` | 小於等於
| `==` | 等於
| `!=` | 不等於

---

## 左移及右移運算子
|  運算子  |   說明    |
|:---:|:----------:|
| `>>` | 邏輯右移
| `<<` | 邏輯左移
| `>>>` | 算術右移
| `<<<` | 算術左移

`在做二的次方倍的乘除法時，我們會使用左移及右移來避免使用乘法器`

>而邏輯移位跟算術移位的差別在於，邏輯移位不管左移還是右移，都會移'0'進來，而算術移位在右移時則會去依照數字的正負號來決定移入'0'還是'1'，正的話移入'0'，負則移入'1'。

---

## 連結運算子
|  運算子  |   說明    |
|:---:|:----------:|
| `{}` | 串接變數

EX:
```
a = {1'b0, 3'd7}        //a = 4'b0111
b = {4{1'b1}};          //b = 4'b1111;
c = {4{1'b1}, 3{1'b0}}; //c = 7'b1111000;

```

---

## 條件運算子
|  運算子  |   說明    |
|:---:|:----------:|
| `?:` | if-else

EX:
```
c = (a==1'b1)?1'b0:1'b1;//等效c = !a，不過合成出來的電路不相同
```

# [Day7] Behavior Level
## always block
- always若超過一行要用begin、end包起來。
- always內的變數若要賦值(等號左邊的變數)必須是reg型態，而等號右邊可以是wire或reg。
- always的觸發條件若超過一項則以 ' , ' 或是' or '  區分。
- 正緣觸發用posedge，負原則是negedge。
- 若想以任意一個訊號b的變動來當作觸發原則是寫成 always @(b)
- 若想隨時執行這個block寫成 always @(*)

EX:
```
always@(posedge clkSys or negedge rst_n)begin
  if(!rst_n)begin
    .....
  end
  else begin
    .....
  end
end

```
---

## if-else語句
這裡的if-else跟C語言是相同的用法，不過值得一提的是，`在寫verilog時else最好要寫，避免電路的描述不完整，容易產生latch。`
EX:
```
if(...)begin
  if()begin
    ....
  end
  else begin
    ....
  end
end
else if(...)begin
  ....
end
else begin
  ....
end
```

---

## case、casex、casez語句
- 一般case中的item不能有'z'或是'x'，只能出現'0'、'1'。
- casez中的item值除了”0”、“1”外，'z'也可以出現。
- casex中的item值除了”0”、“1”外，'z'及'x'都可以出現。
- 最後記得endcase。
- 除外的狀況寫在default內。

EX:
```
case(...)
  item_1:begin
    ....
  end
  item_2:begin
    ....
  end
  item_3:begin
    ....
  end
  item_4:begin
    ....
  end
  default:begin
    ....
  end
endcase
```

`這邊的default跟if-else中的else一樣，不管有沒有用，最好都加上去，避免導致latch。`

# [Day8] for迴圈在硬體的使用及該注意的那些事
## for-loop
在C/C++語言中，我們經常用到for迴圈語句，但在verilog中for語句的使用上會有很大的區別。

Verilog的for迴圈會經常在TestBench上做使用，一方面是TestBench只是拿來測試電路功能的正確性，而且在TestBench中生成信號用for迴圈也比較方便，不需一行一行打。

但是在RTL(Register-Transfer-Level)中卻很少使用for迴圈，其原因是，for迴圈會被綜合器展開為所有變數情況，每個變數獨立佔用暫存器資源，且每條執行語句並不能有效地複用硬體邏輯資源，而這樣的情況會造成消耗大量硬體資源，迴圈次數越多次，整體合成面積會越大，for迴圈雖然方便，但換來的則是速度上的下降。

雖然for迴圈不是那麼理想，但還是有使用的場合，例如`初始化二維陣列`時就需要，因為在verilog中不能直接對整個二維列賦值，此時就需要用index去跑每一個變數。

EX:
```
reg [63:0]mem[255:0];
integer i;

always@(posedge clkSys or negedge rst_n)begin
  if(!rst_n)begin
    for(i=0;i<256;i=i+1)begin//verilog沒有i++的用法
      mem[i] <= 64'd0;
    end
  end
  else begin
    ......
  end

end
```

另一個情況就是就是在時間與面積的trade-off過後，真的有需要在單獨一個clk完成一些處理的需求，也可以適當使用for迴圈，例如雙迴圈，雙迴圈可能耗的硬體資源太多，則可以將外迴圈拆開，一個clk後index才加一，而內迴圈依舊保持。

`總結：雖然for迴圈是可以綜合的，而且效率很高。但所消耗的邏輯資源較大。在對clk數要求不是很高的情況下，可以多用幾個時鐘週期來取代for迴圈的使用。`

# [Day9] Blocking & Non-Blocking的差異
## Blocking vs Non-Blocking
在寫一般軟體語言時，都與verilog中的blocking語句相同，是一行一行由上至下執行的，但verilog又有一個non-blocking，而non-blocking則是同步執行的，而撰寫時也要把握以下原則，才不會產生誤用的情形。
- always用clock觸發的區塊要使用nonblocking。
- always沒有用clock觸發(組合邏輯)的區塊要使用blocking。
- assign語句一律使用blocking。
- 在同一個always block中不可同時出現nonblocking及blocking。

### 來看下面這個例子
`non-blocking:`
```
module blockingVSnonblocking(
  clkSys, 
  rst_n
);
input clkSys;
input rst_n;
reg [1:0]a;
reg [1:0]b;
reg [1:0]c;
always@(posedge clkSys or negedge rst_n)begin
  if(!rst_n)begin
    a <= 2'd1;
    b <= 2'd2;
    c <= 2'd3;
  end
  else begin
    a <= c;
    b <= a;
    c <= b;
  end
end
endmodule

```
`testbench:`
```
`timescale 10ns/1ns
module tb();
reg clkSys;
reg rst_n;

blockingVSnonblocking UUT(
  .clkSys(clkSys), 
  .rst_n(rst_n)
);

initial begin
  clkSys = 0;
  rst_n = 0;
  repeat(3)@(posedge clkSys)rst_n = 0;
  rst_n = 1;
  #100 $stop;
end

always #5 clkSys = ~clkSys;

endmodule

```
上方的執行結果會是
![](https://i.imgur.com/G5L0rGh.png)

>reset後，clk正緣來臨時，程式會同步的執行，所以{a, b, c}的值會同步到{c, b, a}。

---

`blocking:`
```
module blockingVSnonblocking(
  clkSys, 
  rst_n
);
input clkSys;
input rst_n;
reg [1:0]a;
reg [1:0]b;
reg [1:0]c;
always@(*)begin
  if(!rst_n)begin
    a = 2'd1;
    b = 2'd2;
    c = 2'd3;
  end
  else begin
    a = c;
    b = a;
    c = b;
  end
end
endmodule
```
而上方的執行結果則會是
![](https://i.imgur.com/mJ5k6AT.png)

>可以看出{a, b, c}最後都變成了2'd3，這就是使用了non-blocking，使程式變得有順序性，第一行a=c，導致第二行b=a時也等同b=c了，最後一行也是如此。

# [Day10] 模組化及引用模組
## 模組
在一個.V檔案裡面，可以有很多個module，但是Top Module只會有一個，所以檔名必須以Top Module.v來命名來辨別Top Module。

---

## 模組化的概念
- 每個模組的實際意義是一塊實際的硬體電路。
- 每一塊模組都有自己的功能，再透過連接各個模組來達成特定功能。
- 模組與模組之間也會是並行處理的。

---

## 引用模組
為甚麼會需要引用模組呢？

因為通常一個大模組往往會已由許多小模組所組成的，這樣便有以下好處：
- 一方面是可讀性比較好，往後在維護方面也比較方便，因為電路分為一塊一塊的比較好去知道各個模塊的功能，再針對特定的模塊做優化就好。
- 再來是除錯，合成為大電路前，如果可以先提前測試每個模塊功能是否正常，那debug起來也會比較快

---

## 引用模組的兩種方法
將模組的埠與其他模組連接的方法有兩種，分別是:

1.依照要引用之模組的埠列「**順序**」(**in order**)來連接，也就是如果要引用的模組是：
```
module test(
  clkSys, 
  rst_n
);
..
...
....
endmodule
```
那麼引用時括號內的順序就會對應到該模組括號內的順序，例如：
```
reg clk;
reg reset_n;
test U0(clk, reset_n);//module instantiation
```
那麼這個模組的{clk, reset_n}就會接到test模組的{clkSys, rst_n}

2.依「指定名稱」(by name)的方法來連接，會以" .該模組腳位(此模組腳位) "來引用，例如：
```
reg clk;
reg reset_n;
//module instantiation
test U0(
  .clk(clk), 
  .reset_n(reset_n)
);
```
這樣也會是一樣的效果~~

而不管是哪一種引用法引用時都要給模組命名，像這邊就是命名為"U0"

>**對於一個較大的電路來說，可能接腳會非常的多，此時如果以in order的方式來連接，這個時候就算邊看邊打也相當不方便且容易犯錯，所以通常我們會以by name的方法來連接來避免不必要的失誤。**

# [Day11] Function & Task
很多人對於Function以及Task有點混亂，這篇將帶你搞懂他們~

## 共有的特色
- 他們都會寫在Module內而不是外面。
- **都不能使用wire變數**(原因大概也是wire不具有記憶性吧)。
- 被使用時都會放在always內(所以適用Behavioral level)。
- 裡面都不能放always block。

---

## Function
- 可以引用其他的Function，但就是不能引用task。
- 至少要有一個以上的input。
- 最多只能有一個Output。
- 一定出現在等號右邊(有output值)。

EX:
`輸入數字計算需要的位寬大小`
```
function integer log2;
  input integer in ;
  for(log2=0; in>1; log2=log2+1) begin
    in = in >> 1 ;
  end
endfunction
```
引用 log2：
```
parameter width = 16;
reg [log2(width)]:0]a;//5 bit register a
```
`在這邊值得注意的是，上面雖然用了integer，你可能會想，這樣不是很耗硬體資源嗎，不如自己算好log2再打上數字還比較省資源，但其實那些數字會在 "前置處理器" 就先幫你算出來了，所以並不會合成出實際的電路~`
`(在quartus編譯好然後查看RTL Viewer就可以知道了~)`

---

## Task
- 可以引用其他的Function以及task。
- 不一定要宣告input、output，有的話可以有數個都沒問題。

EX:
`七段顯示解碼(共陰)`
```
task give_seg;
  input reg [13:0]in;
  output reg [7:0]out;
  casex(in)
    14'd0:out = 8'b11111100;
    14'd1:out = 8'b01100000;
    14'd2:out = 8'b11011010;
    14'd3:out = 8'b11110010;
    14'd4:out = 8'b01100110;
    14'd5:out = 8'b10110110;
    14'd6:out = 8'b10111110;
    14'd7:out = 8'b11100000;
    14'd8:out = 8'b11111110;
    14'd9:out = 8'b11100110;
    default:out = 8'b00000010;
  endcase
endtask
```

# [Day12] Latch的生成條件以及如何避免(上)
## 什麼是Latch?
前幾篇在if-else以及case語句時有提到latch，那什麼是latch呢？

latch又俗稱"鎖存器"，它是以電平(High or Low)觸發的一種存儲單元，資料儲存動作將取決於致能(enable)的電平值，當latch被致能時，此時輸入會等於輸出，也就是輸出會隨輸入的變化而產生變化，而不被致能時，輸出則與輸入無關，而且保持前態。

說到這裡，那麼flip-flop又是什麼呢？

flip-flop跟latch有點類似，但是flip-flop是邊緣觸發的儲存單元，也就是只有在正緣或負緣這段`極短的時間內`輸出狀態才會改變，它也就是一般我們verilog中reg**期望**它綜合出來的元件。

位甚麼會說是期望？因為reg在綜合時，有時會因為撰寫的程式碼有瑕疵導致reg被綜合成我們不期待的latch。

---

## 為甚麼需要避免latch？
我們再來提到`亞穩態`，它是指觸發器在某一段時間內無法達到一個正確的值，我們又知道"1"到"0"及"0"到"1"不是完全不需要時間的，此時觸發器的輸出端Q在較長時間內處於振盪狀態，且不會等於輸入端D，而這段時間也會被稱做`resolution time`，而其主要原因還是要歸咎於觸發器不夠快，無法滿足setup time/hold time，所以在輸出為一個確定的值之前，它有可能是`毛刺`、`震盪`、`或是其他不穩定的值。`

再來剛剛提到latch被致能時，輸出完全的由輸入來決定，而且這段時間不是很短的，這種情形就會很危險，**很容易將前一級的不穩定狀態傳遞給後級**，導致整體電路發生問題，也會讓時序分析變得更加複雜。

加上latch在FPGA資源中往往需要耗更多資源來合成，耗費更多的面積，那麼以上就是我們詬病latch的原因。

# [Day13] Latch的生成條件以及如何避免(下)
## Latch的生成條件
上一篇講解了什麼是latch，其又與flip-flop差在哪，也解釋了我們要去避免它的原因，那麼這篇將會告訴你有哪幾種情況會產生latch。

---

### 第一種：if語句結構不完整
在組合邏輯中，不完整的if-else語句會導致latch的生成，那怎樣是不完整呢？
就是最後缺少了else，正是因為沒有寫else，會被系統認定為該值不需要改變，而自動生成latch來幫你儲存

**會生成latch的例子：**
```
module latch_test(
  in,
  en, 
  out
);
input  in;
input  en;
output out;
reg    out;

always@(*)begin
  if(en) out = in;//no else
end

endmodule
```
解決方法：
**SOL1：補齊else語句**
```
module latch_test(
  in,
  en, 
  out
);
input  in;
input  en;
output out;
reg    out;

always@(*)begin
  if(en) out = in;
  else   out = 1'b0;//add
end

endmodule
```
**SOL2：在always內最上方加上初值**
```
module latch_test(
  in,
  en, 
  out
);
input  in;
input  en;
output out;
reg    out;

always@(*)begin
  out = 1'b0;//initialization
  if(en) out = in;
end

endmodule
```
**但這裡有一點該注意，其實講補齊if-else算是攏統的說法，因為以下這個例子其實也會產生latch，原因就在同一個變數在不同if條件下都要敘述完整，否則還是會生成latch。**
```
module latch_test(
  in1,
  in2,
  en, 
  out1,
  out2
);
input  in1;
input  in2;
input  en;
output out1;
output out2;
reg    out1;
reg    out2;

always@(*)begin
  if(en) out1 = in1;
  else   out2 = in2;
end

endmodule
```
**應改成**
```
module latch_test(
  in1,
  in2,
  en, 
  out1,
  out2
);
input  in1;
input  in2;
input  en;
output out1;
output out2;
reg    out1;
reg    out2;

always@(*)begin
  if(en)begin
    out1 = in1;
    out2 = 1'b0;//add
  end 
  else begin
    out1 = 1'b0;//add
    out2 = in2;
  end 
end

endmodule
```

`這裡值得注意的是，在循序邏輯中(clock觸發)，不完整的if-else並不會生成latch，因為register具有儲存前態的功能，並只有在正負緣才會改變值~`

---

### 第二種：case語句結構不完整
邏輯跟if-else相似，就是最後缺少了default，正是因為沒有寫default，進入到未被定義的case時會被系統認定為該值不需要改變，而自動生成latch來幫你儲存。

**會生成latch的例子：**

```
module latch_test(
  in1,
  in2,
  sel, 
  out
);
input       in1;
input       in2;
input [1:0] sel;
output      out;
reg         out;

always@(*)begin
  case(sel)
    2'd0:out = in1;
    2'd1:out = in2;
  endcase
end

endmodule
```
**應改成**
```
module latch_test(
  in1,
  in2,
  sel, 
  out
);
input       in1;
input [1:0] sel;
output      out;
reg         out;

always@(*)begin
  case(sel)
    2'd0:out = in1;
    2'd1:out = in2;
    default:out = 1'b0;//add
  endcase
end

endmodule
```
**或是補上初值也是可以的！**

---

### 第三種：使變數等於自己時或是判斷元素有自己時

因為會用到變數的前態而自動生成latch來幫你儲存。

**會生成latch的例子：**
```
reg a;
reg b;

always@(*)begin
  if(a | b)a = 1'b0;
  else     a = 1'b1;
end
```
**以及**
```
reg a;
reg b;
always@(*)begin
  if(b)a = a;
  else a = a + 1'b1;
end
```
**當然wire也不例外**
```
wire a;
wire c;
reg b;
assign a = (a & b)?(1'b0):(1'b1);
assign c = (a & b)?(c):(1'b0);
```
**這種其實不太能解，就是盡量不要這樣寫**

或是，如果輸出沒有非常及時，也可以延後一個clk將值鎖在register內
EX:
```
reg a;
reg b;
reg a_temp;

always@(posedge clkSys)begin
  a_temp <= a;
end

always@(*)begin
  if(a_temp | b)a = 1'b0;
  else          a = 1'b1;
end
```
用先前儲存的a_temp來當判斷元素，也可以避免。

---

### 第四種：always觸發條件不完整
`等號右邊的所有變數或判斷元素都應蓋列入alway觸發條件中`

**會生成latch的例子：**
```
module latch_test(
  in1,
  in2,
  sel, 
  out
);
input  in1;
input  in2;
input  sel;
output out;
reg    out;

always@(in1 or in2)begin
  if(sel)out = in1;
  else   out = in2;
end

endmodule
```
**應改成**
```
module latch_test(
  in1,
  in2,
  sel, 
  out
);
input  in1;
input  in2;
input  sel;
output out;
reg    out;

always@(in1 or in2 or sel)begin//add sel
  if(sel)out = in1;
  else   out = in2;
end

endmodule
```

以上四種就是常見的latch生成原因以及相對應解決的方法~~

# [Day14] verilog中的可綜合語句
> 我們都知道verilog是一種硬體描述語言，所以目的就是要能綜合出實際的電路，但實際上在verilog中並不是所有語句都是可綜合的，因為有些語句是用來驗證(TestBench)的關鍵字，屬於那些驗證用的語句只能在驗證時被使用，例如initial、time、wait...等等，所以在設計數位電路時，一定要特別注意電路的可綜合性~~

**所有綜合工具都支持的語法：**
> always, assign, begin, end, case, wire, tri, aupply0, supply1, reg, integer, default, for, function, and, nand, or, nor, xor, xnor, buf, not, bufif0, bufif1, notif0, notif1, if, inout, input, instantitation, module, negedge, posedge, operators, output, parameter。

**有些綜合工具支持但有些不支持的語法：**
> casex, casez, wand, triand, wor, trior, real, disable, forever, arrays, memories, repeat, task, while。

**所有綜合工具都`不`支持的語法：**
> time, defparam, $finish, fork, join, initial, delays, UDP, wait。

---

## 撰寫可綜合電路應該要保持的幾項原則：
- 不應該使用initial語句來初始化電路的信號。
  - 所有的初始化動作都應該靠reset或是reset_n來達成。
  - 寫MUC並燒錄進去後要按下reset鍵才會動作的原因也是為此，就是為了初始化電路內的各個信號。
  - 由於信號應由reset或reset_n來初始化，所以宣告reg時也不會先給初始值，那是沒有必要的。
- 整體電路應該為同步式設計。
  - 意思是always內的觸發信號應當只有clk以及reset信號。
  - 由電路內自己產生的信號當作是always的觸發是不算同步式設計的。
- 對同一個變數不可同時使用blocking及non-blocking。
  - 前幾篇有提到組合邏輯用blocking，而循序邏輯用non-blocking，依照此原則也不會誤用~
- 不使用UDP(User Defined Primitives)
  - 是一種允許用戶自己定義的元件，通過UDP，可以把一塊組合邏輯電路或者循序邏輯電路封裝在一個UDP內，並把這個UDP作為一個基本的元件來引用，但UDP不能綜合，只能用於仿真。
- 不該在程式內使用delay語句(ex: #100)。
- 同一個變數不可由兩個不同的always來賦值。
- 同一個變數不應該由多個不同的clk來做觸發。
```