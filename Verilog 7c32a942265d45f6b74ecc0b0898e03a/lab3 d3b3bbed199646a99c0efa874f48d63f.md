# lab3

## Lab3

```verilog
module lab3(
		CLOCK_50, 
		KEY,
		IRDA_RX,
		LCD_DATA,
		LCD_EN,
		LCD_RW,
		LCD_RS,
		LCD_ON,
		LCD_BLON
	);
	/*---------Ports Declarations---------*/
	input       CLOCK_50;
	input  [3:0]KEY;
	input	      IRDA_RX;	
	output [7:0]LCD_DATA;
	output      LCD_EN;
	output      LCD_RW;
	output      LCD_RS;
	output      LCD_ON;
	output      LCD_BLON;
	/*---------variables---------*/
	//lab3 module
	wire        sfr_wr;
	wire	      sfr_rd; 
	wire	 [7:0]sfr_addr; 
	wire	 [7:0]sfr_data_out; 
	reg 	 [7:0]sfr_data_in;
	reg    [7:0]curDC;
	reg         flag;
	//ir module
	wire        dataReady;  //ir output dataReady
	wire  [31:0]oDATA;      //ir output data
	//LCD module
	reg         ilcdrs;     //LCD input ilcdrs
	reg	 [7:0]idataLCD;   //LCD input idataLCD
	reg         ilcdOn;     //LCD input ilcdOn
	reg         itick_start;//LCD input itick_start
	wire        olcdReady;  //LCD output olcdReady
	//8051 module
	wire        por_n;
	wire        rst_out_n;
	wire        test_mode_n;
	wire        stop_mode_n;
	wire        idle_mode_n;
	wire  [15:0]mem_addr; 
   wire   [7:0]mem_data_out; 
   wire   [7:0]mem_data_in; 
   wire        mem_wr_n; 
   wire        mem_rd_n; 
   wire        mem_pswr_n;    
   wire        mem_psrd_n; 
   wire        mem_ale; 
   wire        mem_ea_n;// 1: external ROM ,0: Internal ROM
   wire        int0_n; 
   wire        int1_n; 
   wire        int2; 
   wire        int3_n; 
   wire        int4; 
   wire        int5_n; 
   wire        pfi; 
   wire        wdti; 
   wire        rxd0_in; 
   wire        rxd0_out, txd0; 
   wire        rxd1_in; 
   wire        rxd1_out, txd1; 
   wire        t0; 
   wire        t1; 
   wire        t2; 
   wire        t2ex; 
   wire        t0_out, t1_out, t2_out; 
   wire        port_pin_reg_n, p0_mem_reg_n, p0_addr_data_n, p2_mem_reg_n; 
   wire   [7:0]iram_addr, iram_data_out, iram_data_in; 
   wire        iram_rd_n, iram_we1_n, iram_we2_n; 
   wire  [15:0]irom_addr; 
   wire   [7:0]irom_data_out; 
   wire        irom_rd_n, irom_cs_n;
	/*---------8051_core module instantiation---------*/	
	DW8051_core U0(
		.clk(CLOCK_50),
		.por_n(KEY[0]),
		.rst_in_n(KEY[0]),
		.rst_out_n(rst_out_n),
		.test_mode_n(test_mode_n),
		.stop_mode_n(stop_mode_n),
		.idle_mode_n(idle_mode_n),
		.sfr_addr(sfr_addr),//in
		.sfr_data_out(sfr_data_out),//out
		.sfr_data_in(sfr_data_in),//
		.sfr_wr(sfr_wr),//
		.sfr_rd(sfr_rd),//
		.mem_addr(mem_addr),
		.mem_data_out(mem_data_out),
		.mem_data_in(mem_data_in),
		.mem_wr_n(mem_wr_n),
		.mem_rd_n(mem_rd_n),
		.mem_pswr_n(mem_pswr_n),
		.mem_psrd_n(mem_psrd_n),
		.mem_ale(mem_ale),
		.mem_ea_n(mem_ea_n),
		.int0_n(int0_n),
		.int1_n(int1_n),
		.int2(int2),
		.int3_n(int3_n),
		.int4(int4),
		.int5_n(int5_n),
		.pfi(pfi),
		.wdti(wdti),
		.rxd0_in(rxd0_in),
		.rxd0_out(rxd0_out),
		.txd0(txd0),
		.rxd1_in(rxd1_in),
		.rxd1_out(rxd1_out),
		.txd1(txd1),
		.t0(t0),
		.t1(t1),
		.t2(t2),
		.t2ex(t2ex),
		.t0_out(t0_out),
		.t1_out(t1_out),
		.t2_out(t2_out),
		.port_pin_reg_n(port_pin_reg_n),
		.p0_mem_reg_n(p0_mem_reg_n),
		.p0_addr_data_n(p0_addr_data_n),
		.p2_mem_reg_n(p2_mem_reg_n),
		.iram_addr(iram_addr),
		.iram_data_out(iram_data_out),
		.iram_data_in(iram_data_in),
		.iram_rd_n(),
		.iram_we1_n(iram_we1_n),
		.iram_we2_n(iram_we2_n),
		.irom_addr(irom_addr),
		.irom_data_out(irom_data_out),
		.irom_rd_n(irom_rd_n),
		.irom_cs_n(irom_cs_n)
	);
	assign mem_ea_n = 1'b1;
	/*
	int_mem u3_int_mem(
		.clk(CLOCK_50),
		.addr(iram_addr),
		.data_in(iram_data_in),
		.data_out(iram_data_out),
		.we1_n(iram_we1_n),
		.we2_n(iram_we2_n),
		.rd_n(iram_rd_n)
	);
	*/
	ram	ram_inst (
	.address ( iram_addr ),
	.clock ( ~CLOCK_50 ),
	.data ( iram_data_in ),
	.wren ( ~iram_we2_n ),
	.q ( iram_data_out )
	);
	/*                                  
   ext_mem u2_ext_mem(
		.addr(mem_addr),
		.data_in(mem_data_out),
		.data_out(mem_data_in),
		.wr_n(mem_wr_n),
		.rd_n(mem_rd_n)
	);  */   
	/*	
   rom_mem u4_rom_mem(
		.addr(irom_addr),
		.data_out(irom_data_out),                    
		.rd_n(irom_rd_n),
		.cs_n(irom_cs_n)
	); 
	*/
	rom	rom_inst (
	.address ( irom_addr ),
	.clken ( ~irom_rd_n ),
	.clock ( CLOCK_50 ),
	.q ( irom_data_out )
	);
	
	
	
	/*---------IR_RX module instantiation---------*/	
	IR_RX U5(
		.clkSys(CLOCK_50), 
		.rst_n(KEY[0]), 
		.iIRDA(IRDA_RX),
		.dataReady(dataReady), 
		.data_o(oDATA)
	);
	/*---------IR_RX module instantiation---------*/	
	LCD_control U6(
		.iclkSys(CLOCK_50), 
		.irst_n(KEY[0]),
		.ilcdrs(ilcdrs),		
		.idataLCD(idataLCD), 
		.ilcdOn(ilcdOn),
		.itick_start(itick_start), 
		.odataLCD(LCD_DATA),  
		.olcdrs(LCD_RS), 
		.olcdrw(LCD_RW), 
		.olcdOn(LCD_ON), 
		.olcdEN(LCD_EN),
		.olcdReady(olcdReady)
	);
	/*---------assign wire---------*/	
	assign LCD_BLON      = 1'b1;
	assign test_mode_n   = 1'b1;
	//IR---->0xDA, 0xDB, 0xDC, 0xDD
	//0xDB:
	always@(posedge CLOCK_50 or negedge KEY[0])begin
		if(!KEY[0])begin
			sfr_data_in <= 8'd0;
			flag        <= 1'b0;
		end
		else begin
			if(dataReady)begin
				sfr_data_in <= 8'd3;
				flag        <= 1'b0;
			end 
			else if(sfr_addr == 8'hDB)sfr_data_in <= 8'd0;
			else if(sfr_addr == 8'hDD)sfr_data_in <= 8'd0;
			else if(sfr_addr == 8'hDA && curDC ==8'h03)begin
				sfr_data_in <= 8'd0;
				flag        <= 1'b1;
			end 
			else if((sfr_data_in != 8'd3) && !flag)begin
				case(curDC)
					8'd0:sfr_data_in <= oDATA[7:0];
					8'd1:sfr_data_in <= oDATA[15:8];
					8'd2:sfr_data_in <= oDATA[23:16];
					8'd3:sfr_data_in <= oDATA[31:24];
				endcase
			end
			else sfr_data_in <= sfr_data_in;
		end
	end
	//0xDC:
	always@(posedge CLOCK_50 or negedge KEY[0])begin
		if(!KEY[0])curDC     <= 8'd0;
		else begin
			if(sfr_addr == 8'hDC && sfr_wr)curDC <= sfr_data_out;
			else                           curDC <= curDC;
		end
	end
	
	//LCD---->0xD1, 0xD2, 0xD3:
	//0xD1:
	//---lcd On/off---
	always@(posedge CLOCK_50 or negedge KEY[0])begin
		if(!KEY[0])ilcdOn <= 1'b0;
		else begin
			if(sfr_addr == 8'hD1 && sfr_wr)begin
				if(sfr_data_out[4])      ilcdOn <= 1'b1;
				else if(!sfr_data_out[4])ilcdOn <= 1'b0;
				else                     ilcdOn <= ilcdOn;
			end
			else ilcdOn <= ilcdOn;
		end
	end
	//---lcd rs---
	//rs-->0:write instruction, rs-->1:write data,
	always@(posedge CLOCK_50 or negedge KEY[0])begin
		if(!KEY[0])ilcdrs <= 1'b0;
		else begin
			if(sfr_addr == 8'hD1 && sfr_wr)begin
				if(sfr_data_out[1:0] == 2'b01)     ilcdrs <= 1'b0;//write command
				else if(sfr_data_out[1:0] == 2'b10)ilcdrs <= 1'b1;//write data
				else                               ilcdrs <= ilcdrs;
			end
			else ilcdrs <= ilcdrs;
		end
	end
	//0xD3:lock data from sfr_data_out
	always@(posedge CLOCK_50 or negedge KEY[0])begin
		if(!KEY[0])begin
			itick_start <= 1'b0;
			idataLCD    <= 8'd0;
		end
		else begin
			if(sfr_addr == 8'hD3 && sfr_wr)begin
				itick_start <= 1'b1;
				idataLCD    <= sfr_data_out;
			end
			else begin
				itick_start <= 1'b0;
				idataLCD    <= idataLCD;
			end 
		end
	end
	endmodule
```

## IR_RX(perfect)

```verilog
//`include "stableSingal.v"
	module IR_RX(clkSys, rst_n, iIRDA, dataReady, data_o);
	/*---------parameter Declarations---------*/
	//===== state machine =====//
	localparam IDLE     = 2'd0;
	localparam CUSTOMER = 2'd1;
	localparam DATAREAD = 2'd2;
	localparam CHECKOUT = 2'd3;
	//===== during time =====//
	localparam Leader_LOW  = 18'd229376;//4.587ms
	localparam Leader_HIGH = 18'd204800;//4.096ms
	localparam MaxDataHIGH = 18'd196608;//3.932ms
	localparam DataBitHIGH = 18'd40960; //0.8192ms
	/*---------Ports Declarations---------*/
	input        clkSys;
	input        rst_n;
	input        iIRDA;
	output       dataReady;
	output [31:0]data_o;
	reg          dataReady;
	reg    [31:0]data_o;
	/*---------variables---------*/
	reg     [1:0]fstate;
	reg    [17:0]counter;
	reg    [31:0]databuf;
	reg          counterEN;
	reg          flag_get;
	wire         iRDA_TX;
	/*---------stableSingal instantiation---------*/
	stableSingal U0(
		.clkSys(clkSys), 
		.rst_n(rst_n), 
		.Din(iIRDA), 
		.Dout(iRDA_TX)
	);
	/*---------counter---------*/
	always@(posedge clkSys or negedge rst_n)begin
		if(!rst_n)         counter <= 18'd0;
		else if(counterEN) counter <= counter + 18'd1;
		else               counter <= 18'd0;
	end
	/*---------fstate_state---------*/
	always@(posedge clkSys or negedge rst_n)begin
		if(!rst_n)fstate <= IDLE;
		else begin
			case(fstate)
				IDLE:begin
					if(counter == Leader_LOW) fstate <= CUSTOMER;
					else                      fstate <= IDLE;
				end
				CUSTOMER:begin
					if(counter == Leader_HIGH)fstate <= DATAREAD;
					else                      fstate <= CUSTOMER;
				end
				DATAREAD:begin
					if(counter == MaxDataHIGH)fstate <= CHECKOUT;
					else                      fstate <= DATAREAD;
				end
				CHECKOUT:fstate <= IDLE;
				default: fstate <= IDLE;
			endcase
		end
	end
	/*---------fstate_output---------*/
	always@(posedge clkSys or negedge rst_n)begin
		if(!rst_n)counterEN <= 1'b0; 
		else begin
			case(fstate)
				IDLE:begin
					if(!iRDA_TX&&counter<Leader_LOW)counterEN <= 1'b1;
					else                            counterEN <= 1'b0;
				end
				CUSTOMER:begin
					if(iRDA_TX&&counter<Leader_HIGH)counterEN  <= 1'b1;
					else                            counterEN  <= 1'b0;
				end
				DATAREAD:begin
					if(iRDA_TX&&counter<MaxDataHIGH)counterEN  <= 1'b1;
					else                            counterEN  <= 1'b0;
				end
				CHECKOUT:counterEN <= 1'b0;
				default:counterEN  <= 1'b0;
			endcase
		end
	end
	/*---------counter---------*/
	always@(posedge clkSys or negedge rst_n)begin
		if(!rst_n)flag_get <= 1'b0;
		else      flag_get <= iRDA_TX;
	end
	/*---------put data in databuf---------*/
	always@(posedge clkSys or negedge rst_n)begin
		if(!rst_n)databuf <= 32'd0;
		else begin
			if(fstate > CUSTOMER)begin
				if(!iRDA_TX&&flag_get&&(counter > DataBitHIGH))     databuf <= {1'b1, databuf[31:1]};    
				else if(!iRDA_TX&&flag_get&&(counter < DataBitHIGH))databuf <= {1'b0, databuf[31:1]};
				else                                                databuf <= databuf;
			end
			else databuf <= 32'd0;
		end 
	end
	/*---------check data and sent dataReady---------*/
	always@(posedge clkSys or negedge rst_n)begin
		if(!rst_n)begin
			data_o    <= 32'd0; 
			dataReady <= 1'b0;
		end 
		else begin
			if(fstate==CHECKOUT)begin
				if(databuf[31:24]==~databuf[23:16])begin
					data_o    <= databuf;
					dataReady <= 1'b1;
				end 
				else begin
					data_o    <= data_o;
					dataReady <= 1'b0;
				end 
			end
			else begin
				data_o    <= data_o;
				dataReady <= 1'b0;
			end
		end
	end
	
	endmodule
```

## LCD_control

```verilog
module LCD_control(
		iclkSys, 
		irst_n,
		ilcdrs,
		ilcdOn,		
		idataLCD,
		itick_start, 
		odataLCD,  
		olcdrs, 
		olcdrw, 
		olcdOn, 
		olcdEN,
		olcdReady
	);
	/*---------parameter Declarations---------*/
	//=======state=======//
	localparam IDLE    = 2'd0;
	localparam SENT    = 2'd1;
	localparam MAKEEN  = 2'd2;
	localparam FINISH  = 2'd3;
	//=======cnt=======//
	localparam cntStop = 12'd1024;
	localparam cntRise = 12'd128;
	localparam cntFall = 12'd768;
	/*---------Ports Declarations---------*/
	input       iclkSys;
	input 	   irst_n; 
	input       ilcdrs;
	input       ilcdOn;
	input	 [7:0]idataLCD; 
	input	      itick_start; 
	output [7:0]odataLCD; 
	output	   olcdrs; 
	output	   olcdrw; 
	output	   olcdOn; 
	output	   olcdReady;
	output      olcdEN;
	reg    [7:0]odataLCD;
	reg	      olcdReady;
	reg         olcdEN;
	wire	      olcdrs; 
	wire	      olcdrw; 
	wire	      olcdOn;
	/*---------variables---------*/
	reg    [1:0]fstate;
	reg   [11:0]counter;
	reg         counterEN;
	reg         tick;
	reg         SHEN;
	/*---------assign wire---------*/
	assign olcdrs = ilcdrs;
	assign olcdOn = ilcdOn;
	assign olcdrw = 1'b0;
	/*---------counter---------*/
	always@(posedge iclkSys or negedge irst_n)begin
		if(!irst_n)counter <= 12'd0;
		else begin
			if(counterEN)begin
				if(counter < cntStop)counter <= counter + 12'd1;
				else                 counter <= 12'd0;
			end
			else counter <= 12'd0;
		end
	end
	/*---------tick---------*/
	always@(posedge iclkSys or negedge irst_n)begin
		if(!irst_n)tick <= 1'b0;
		else begin
			if(counter == cntStop-12'd1)tick <= 1'b1;
			else                        tick <= 1'b0;
		end
	end
	/*---------olcdEN---------*/
	always@(posedge iclkSys or negedge irst_n)begin
		if(!irst_n)olcdEN <= 1'b0;
		else begin
			if(SHEN)begin
				if(counter == cntRise)     olcdEN <= 1'b1;
				else if(counter == cntFall)olcdEN <= 1'b0;
				else                       olcdEN <= olcdEN;
			end
			else olcdEN <= 1'b0;
		end
	end
	/*---------fstate state---------*/
	always@(posedge iclkSys or negedge irst_n)begin
		if(!irst_n)fstate <= IDLE;
		else begin
			case(fstate)
				IDLE:begin
					if(itick_start)fstate <= SENT;
					else           fstate <= IDLE;
				end
				SENT:begin
					fstate <= MAKEEN;
				end
				MAKEEN:begin
					if(tick)fstate <= FINISH;
					else    fstate <= MAKEEN;
				end
				FINISH:begin
					if(tick)fstate <= IDLE;
					else    fstate <= FINISH;
				end
				default:fstate <= IDLE;
			endcase
		end
	end
	/*---------fstate output---------*/
	always@(posedge iclkSys or negedge irst_n)begin
		if(!irst_n)begin
			counterEN <= 1'b0;
			olcdReady <= 1'b0;
			odataLCD  <= 8'd0;
			SHEN      <= 1'b0;
		end
		else begin
			case(fstate)
				IDLE:begin
					counterEN <= 1'b0;
					olcdReady <= 1'b0;
					SHEN      <= 1'b0;
				end
				SENT:begin
					counterEN <= 1'b0;
					olcdReady <= 1'b0;
					odataLCD  <= idataLCD;
					SHEN      <= 1'b0;
				end
				MAKEEN:begin
					counterEN <= 1'b1;
					olcdReady <= 1'b0;
					SHEN      <= 1'b1;
				end
				FINISH:begin
					counterEN <= 1'b1;
					if(tick)olcdReady <= 1'b1;
					else    olcdReady <= 1'b0;
					SHEN      <= 1'b0;
				end
				default:begin
					counterEN <= 1'b0;
					olcdReady <= 1'b0;
					odataLCD  <= 8'd0;
					SHEN      <= 1'b0;
				end
			endcase
		end
	end
	endmodule
```

## TestBench

```verilog
`timescale 1ns/1ns

module lab3_tb;

parameter DATA_LO_PERIOD      = 560_000; // 560us
parameter DATA0_HI_PERIOD  = 560_000; // 560us
parameter DATA1_HI_PERIOD  = 1690_000; // 560us

reg clk_50M;
reg reset_n;
wire [3:0] key;
reg ir;
wire [6:0] hex0;
wire [6:0] hex1;
wire [6:0] hex2;
wire [6:0] hex3;
wire [6:0] hex4;
wire [6:0] hex5;
wire [6:0] hex6;
wire [6:0] hex7;
wire [7:0] LCD_DATA;
wire       LCD_EN;
wire       LCD_RW;
wire       LCD_RS;
wire       LCD_ON;
wire       LCD_BLON;

lab3 u1(
		.CLOCK_50(clk_50M), 
		.KEY(key),
		.IRDA_RX(ir),
		.LCD_DATA(LCD_DATA),
		.LCD_EN(LCD_EN),
		.LCD_RW(LCD_RW),
		.LCD_RS(LCD_RS),
		.LCD_ON(LCD_ON),
		.LCD_BLON(LCD_BLON)
	);

always
  #10 clk_50M = ~clk_50M;
  
always@(negedge LCD_EN)
    begin // 
    if(!LCD_RW) // LCD Write
      begin
      if(LCD_RS)
        $display("time=%3d,LCD w_data = 0x%x ,'%C' ", $time,LCD_DATA,LCD_DATA);
      else
        $display("time=%3d,LCD w_cmd = 0x%x ", $time,LCD_DATA);
      end
    else        // LCD Read
      begin
      if(LCD_RS)
        $display("time=%3d,LCD r_data = 0x%x ,'%C' ", $time,LCD_DATA,LCD_DATA);
      else
        $display("time=%3d,LCD r_cmd = 0x%x ", $time,LCD_DATA);
      end
    end
  
assign key[0] = reset_n;
assign key[1] = reset_n;
assign key[2] = reset_n;
assign key[3] = reset_n;

initial
  begin
  reset_n = 0;  
  clk_50M = 0 ;
  ir = 0;
  
  #30 reset_n = 1;
  
  // ir random noise
  #1_000_000;
  ir = 1;
  #20_000_000;
  ir = 0;
  #1_000_000;
  ir = 1;
  #3_000_000;

  // ir packet 
  #10_000_000; 
  send_ir_leader();  
  send_ir_byte(8'h12);   // customer code
  send_ir_byte(8'h34);   // customer code
  send_ir_byte(8'h56);   // key code
  send_ir_byte(8'hA9);   // key code reverse
  send_ir_end();       
  
  #10_000_000; 
  send_ir_leader();  
  send_ir_byte(8'h68);   
  send_ir_byte(8'hB6);   
  send_ir_byte(8'hF0);   
  send_ir_byte(8'h0F);   
  send_ir_end();  
  
  #10_000_000;         
  
  
  $stop;
  end
  
//initial
//  begin
//  $monitor("time=%3d reset_n=%d hex0=%x hex1=%x hex2=%x hex3=%x hex4=%x hex5=%x hex6=%x hex7=%x",$time,reset_n,hex0,hex1,hex2,hex3,hex4,hex5,hex6,hex7);
// end
  
task send_ir_leader;  
 begin
  ir = 0;
  #9_000_000; // 9ms Lo
  ir = 1;
  #4_500_000; // 4.5ms Hi
 end
endtask 

task send_ir_byte;
  input [7:0] byte;
  integer i;
  begin
  $display("send ir = %x ",byte);
  for (i=0; i<8; i=i+1) 
     begin 
     ir = 0;
     #(DATA_LO_PERIOD);
     ir = 1;
     if(byte[0])
       #(DATA1_HI_PERIOD);
     else
       #(DATA0_HI_PERIOD); 
     byte = byte >> 1;       
     end      
  end   
endtask 

task send_ir_end;  
 begin
  ir = 0;
  #(DATA_LO_PERIOD);  
  ir = 1;  
 end
endtask
  
endmodule
```

## Wave

![lab3%20d3b3bbed199646a99c0efa874f48d63f/123456A9.png](lab3%20d3b3bbed199646a99c0efa874f48d63f/123456A9.png)

![lab3%20d3b3bbed199646a99c0efa874f48d63f/68B6F00F.png](lab3%20d3b3bbed199646a99c0efa874f48d63f/68B6F00F.png)

## Monitor

![lab3%20d3b3bbed199646a99c0efa874f48d63f/Untitled.png](lab3%20d3b3bbed199646a99c0efa874f48d63f/Untitled.png)

![lab3%20d3b3bbed199646a99c0efa874f48d63f/Untitled%201.png](lab3%20d3b3bbed199646a99c0efa874f48d63f/Untitled%201.png)

![lab3%20d3b3bbed199646a99c0efa874f48d63f/Untitled%202.png](lab3%20d3b3bbed199646a99c0efa874f48d63f/Untitled%202.png)