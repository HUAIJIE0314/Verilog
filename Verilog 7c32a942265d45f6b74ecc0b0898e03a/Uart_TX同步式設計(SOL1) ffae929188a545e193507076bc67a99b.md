# Uart_TX同步式設計(SOL1)

## Uart_TX

```verilog
	module Uart_TX(clk_sys, rst_n, en, din, busy, TX_D, finish);
	/*----------parameter---------*/
	parameter IDLE     = 2'd0;
	parameter start    = 2'd1;
	parameter data_TX  = 2'd2;
	parameter stop     = 2'd3;
	/*--------input/output--------*/
	input       clk_sys, en, rst_n;
	input       [7:0]din;
	output reg  TX_D, finish;
	output wire busy;
	/*----------variables---------*/
	reg [1:0]fstate;
	reg [12:0]cnt;
	reg [2:0]pos;
	reg [7:0]data;
	reg tick_uart;
	/*----------------------------*/
	assign busy = (fstate != IDLE);
	/*----------9600HZ------------*/
	always@(posedge clk_sys or negedge rst_n)begin
		if(!rst_n)begin
			cnt <= 13'd0;
		end 
		else begin
			if(cnt<13'd5207)cnt <= cnt + 13'd1;
			else cnt <= 13'd0;
		end
	end
	/*----------Uart tick------------*/
	always@(posedge clk_sys or negedge rst_n)begin
		if(!rst_n)tick_uart <= 1'b0;
		else begin
			if(cnt==13'd5206)tick_uart <= 1'b1;
			else             tick_uart <= 1'b0;
		end
	end
	/*-----------state------------*/
	always@(posedge clk_sys or negedge rst_n)begin
		if(!rst_n)fstate <= IDLE;
		else begin
			case(fstate)
				IDLE:begin
					if(en)fstate<=start;
					else  fstate<=IDLE;
				end
				start:begin
					if(tick_uart==1'b1)fstate <= data_TX;
					else               fstate <= start;
				end
				data_TX:begin
					if(pos==3'd7&&tick_uart==1'b1)fstate <= stop;
					else                          fstate <= data_TX;
				end
				stop:begin
					if(tick_uart==1'b1)fstate <= IDLE;
					else               fstate <= stop;
				end
				default:fstate <= IDLE;
			endcase
		end
	end
	/*-----------output-----------*/
	always@(posedge clk_sys or negedge rst_n)begin
		if(!rst_n)begin
			TX_D <= 1'b1;
			pos  <= 3'd0;
			data <= 8'd0;
		end
		else begin
			if(tick_uart==1'b1)begin
				case(fstate)
					IDLE:begin
						TX_D <= 1'b1;
					end
					start:begin
						data <= din;
						TX_D <= 1'b0;
						pos  <= 3'd0;
					end
					data_TX:begin
						TX_D <= data[0];
						data <= data>>1;//right rotate
						pos  <= pos + 3'd1;
					end
					stop:begin
						TX_D <= 1'b1;
					end
					default:TX_D <= 1'b0;
				endcase
			end
			else begin
				TX_D <= TX_D;
				pos  <= pos;
				data <= data;
			end
		end
	end
	always@(posedge clk_sys or negedge rst_n)begin
		if(!rst_n)finish <= 1'b0;
		else begin
			if(fstate==stop&&tick_uart)finish <= 1'b1;
			else                       finish <= 1'b0;
		end
	end
	endmodule
```

## TestBench

```verilog
	`timescale 10ns/1ns
	module tb_Uart_TX();
	
	reg clk_sys, en, rst_n;
	reg [7:0]din;
	wire busy, TX_D;
	wire finish;
	reg [12:0]cnt  = 13'd0;
	reg [7:0]data[3:0];
	reg [1:0]cnt2 = 2'd0;
	Uart_TX U0(
		.clk_sys(clk_sys), 
		.rst_n(rst_n),
		.en(en),
		.din(din), 
		.busy(busy),
		.TX_D(TX_D),
		.finish(finish)
	);
	always@(posedge clk_sys or negedge rst_n)begin
		if(!rst_n)begin
			cnt <= 13'd0;
		end 
		else begin
			if(cnt<13'd5208)cnt <= cnt + 13'd1;
			else cnt <= 13'd0;
		end
	end
	initial begin
		clk_sys = 1'b0;
		en = 1'b0;
		rst_n = 1'b1;
		repeat(5)@(posedge clk_sys) rst_n <= 1'b0;
		rst_n <= 1'b1;
		en <= 1'b1;
		data[3] <= 8'b01001101;
		data[2] <= 8'b11001100;
		data[1] <= 8'b00100100;
		data[0] <= 8'b11110000;
	end
	
	always begin
		#10 clk_sys <= ~clk_sys;
	end
	
	always@(posedge clk_sys)begin
		//if(cnt==13'd5207)begin
			if(!busy)begin
				din <= data[cnt2];
				cnt2 <= cnt2 + 2'd1;
			end
		//end
	end
	initial begin
		#1000_0000 $stop;
	end
	initial begin
	  $monitor("time=%3d,TX_D=%d",$time,TX_D);
	end
	
	endmodule
```

---

## Wave

![Uart_TX%E5%90%8C%E6%AD%A5%E5%BC%8F%E8%A8%AD%E8%A8%88(SOL1)%20ffae929188a545e193507076bc67a99b/Untitled.png](Uart_TX%E5%90%8C%E6%AD%A5%E5%BC%8F%E8%A8%AD%E8%A8%88(SOL1)%20ffae929188a545e193507076bc67a99b/Untitled.png)

## Compilation Report

![Uart_TX%E5%90%8C%E6%AD%A5%E5%BC%8F%E8%A8%AD%E8%A8%88(SOL1)%20ffae929188a545e193507076bc67a99b/Untitled%201.png](Uart_TX%E5%90%8C%E6%AD%A5%E5%BC%8F%E8%A8%AD%E8%A8%88(SOL1)%20ffae929188a545e193507076bc67a99b/Untitled%201.png)

## RTL Viewer

![Uart_TX%E5%90%8C%E6%AD%A5%E5%BC%8F%E8%A8%AD%E8%A8%88(SOL1)%20ffae929188a545e193507076bc67a99b/Untitled%202.png](Uart_TX%E5%90%8C%E6%AD%A5%E5%BC%8F%E8%A8%AD%E8%A8%88(SOL1)%20ffae929188a545e193507076bc67a99b/Untitled%202.png)

## State Machine

![Uart_TX%E5%90%8C%E6%AD%A5%E5%BC%8F%E8%A8%AD%E8%A8%88(SOL1)%20ffae929188a545e193507076bc67a99b/Untitled%203.png](Uart_TX%E5%90%8C%E6%AD%A5%E5%BC%8F%E8%A8%AD%E8%A8%88(SOL1)%20ffae929188a545e193507076bc67a99b/Untitled%203.png)