# IR_RX improve

```verilog
`include "stableSingal.v"
	module IR_RX(clkSys, rst_n, iIRDA, dataReady, data_o);
	/*---------parameter Declarations---------*/
	//===== state machine =====//
	localparam IDLE     = 2'd0;
	localparam CUSTOMER = 2'd1;
	localparam DATAREAD = 2'd2;
	localparam CHECKOUT = 2'd3;
	//===== during time =====//
	localparam Leader_LOW  = 18'd229376;//4.587ms
	localparam Leader_HIGH = 18'd204800;//4.096ms
	localparam MaxDataHIGH = 18'd196608;//3.932ms
	localparam DataBitHIGH = 18'd40960; //0.8192ms
	/*---------Ports Declarations---------*/
	input        clkSys;
	input        rst_n;
	input        iIRDA;
	output       dataReady;
	output [31:0]data_o;
	reg          dataReady;
	reg    [31:0]data_o;
	/*---------variables---------*/
	reg     [1:0]fstate;
	reg    [17:0]counter;
	reg    [31:0]databuf;
	reg     [1:0]flag_cnt;
	reg          counterEN;
	reg          flag_get;
	wire         iRDA_TX;
	/*---------stableSingal instantiation---------*/
	stableSingal U0(
		.clkSys(clkSys), 
		.rst_n(rst_n), 
		.Din(iIRDA), 
		.Dout(iRDA_TX)
	);
	/*---------counter---------*/
	always@(posedge clkSys or negedge rst_n)begin
		if(!rst_n)         counter <= 18'd0;
		else if(counterEN) counter <= counter + 18'd1;
		else               counter <= 18'd0;
	end
	/*---------fstate_state---------*/
	always@(posedge clkSys or negedge rst_n)begin
		if(!rst_n)fstate <= IDLE;
		else begin
			case(fstate)
				IDLE:begin
					if(counter == Leader_LOW) fstate <= CUSTOMER;
					else                      fstate <= IDLE;
				end
				CUSTOMER:begin
					if(counter == Leader_HIGH)fstate <= DATAREAD;
					else                      fstate <= CUSTOMER;
				end
				DATAREAD:begin
					if(counter == MaxDataHIGH)fstate <= CHECKOUT;
					else                      fstate <= DATAREAD;
				end
				CHECKOUT:fstate <= IDLE;
				default: fstate <= IDLE;
			endcase
		end
	end
	/*---------fstate_output---------*/
	always@(posedge clkSys or negedge rst_n)begin
		if(!rst_n)counterEN <= 1'b0; 
		else begin
			case(fstate)
				IDLE:begin
					if(!iRDA_TX&&counter<Leader_LOW)counterEN <= 1'b1;
					else                            counterEN <= 1'b0;
				end
				CUSTOMER:begin
					if(iRDA_TX&&counter<Leader_HIGH)counterEN  <= 1'b1;
					else                            counterEN  <= 1'b0;
				end
				DATAREAD:begin
					if(iRDA_TX&&counter<MaxDataHIGH)counterEN  <= 1'b1;
					else                            counterEN  <= 1'b0;
				end
				CHECKOUT:counterEN <= 1'b0;
				default:counterEN  <= 1'b0;
			endcase
		end
	end
	/*---------counter---------*/
	always@(posedge clkSys or negedge rst_n)begin
		if(!rst_n)flag_get <= 1'b0;
		else      flag_get <= iRDA_TX;
	end
	/*---------put data in databuf---------*/
	always@(posedge clkSys or negedge rst_n)begin
		if(!rst_n)databuf <= 32'd0;
		else begin
			if(fstate > CUSTOMER)begin
				if(!iRDA_TX&&flag_get&&(counter>DataBitHIGH))       databuf <= {1'b1, databuf[31:1]};    
				else if(!iRDA_TX&&flag_get&&(counter < DataBitHIGH))databuf <= {1'b0, databuf[31:1]};
				else                                                databuf <= databuf;
			end
			else databuf <= 32'd0;
		end 
	end
	/*---------check data and sent dataReady---------*/
	always@(posedge clkSys or negedge rst_n)begin
		if(!rst_n)begin
			data_o    <= 32'd0; 
			dataReady <= 1'b0;
		end 
		else begin
			if(fstate==CHECKOUT)begin
				if(databuf[31:24]==~databuf[23:16])begin
					data_o    <= databuf;
					dataReady <= 1'b1;
				end 
				else begin
					data_o    <= data_o;
					dataReady <= 1'b0;
				end 
			end
			else begin
				data_o    <= data_o;
				dataReady <= 1'b0;
			end
		end
	end
	
	endmodule
```

## stableSignal

```verilog
module stableSingal(clkSys, rst_n, Din, Dout);
		/*---------Ports Declarations---------*/	
	input  clkSys;
	input  rst_n;
	input  Din;
	output Dout;
	reg    Dout;
	/*---------variables---------*/
	reg [3:0]dly;
	wire low, high;
	assign high = &dly;
	assign low  = |dly;
	/*---------shifting---------*/
	always@(posedge clkSys, negedge rst_n)begin
		if(!rst_n)dly <= 4'd0;
		else dly <= {dly[2:0], Din};
	end
	/*---------output---------*/
	always@(posedge clkSys, negedge rst_n)begin
		if(!rst_n)Dout <= 1'b0;
		else begin
			if(high)    Dout <= 1'b1;
			else if(low)Dout <= 1'b0;
			else        Dout <= Dout;
		end
	end
	endmodule
```

## TestBench

```verilog
`timescale 1ns/1ns

module lab3_tb;

parameter DATA_LO_PERIOD      = 560_000; // 560us
parameter DATA0_HI_PERIOD  = 560_000; // 560us
parameter DATA1_HI_PERIOD  = 1690_000; // 560us

reg clk_50M;
reg reset_n;
wire [3:0] key;
reg ir;
/*
wire [6:0] hex0;
wire [6:0] hex1;
wire [6:0] hex2;
wire [6:0] hex3;
wire [6:0] hex4;
wire [6:0] hex5;
wire [6:0] hex6;
wire [6:0] hex7;
*/
wire dataReady;
wire [31:0]oDATA;
wire [1:0]stateOut;
wire cntEN_IDLE, cntEN_Customer, cntEN_ReadData;
wire [17:0]cnt_IDLE, cnt_Customer, cnt_ReadData;
wire [5:0]bitcount;
wire [31:0]databuf;
/*
lab3 u1(
                //////// CLOCK //////////
                .CLOCK_50(clk_50M),
                .KEY(key),
                //////// SEG7 //////////
                .HEX0(hex0),
                .HEX1(hex1),
                .HEX2(hex2),
                .HEX3(hex3),
                .HEX4(hex4),
                .HEX5(hex5),
                .HEX6(hex6),
                .HEX7(hex7),
                        //////// IR Receiver //////////
                .IRDA_RXD(ir)                      
        );  
*/

IR_RX UUT(
	.clkSys(clk_50M), 
	.rst_n(key[0]), 
	.iIRDA(ir),
	.dataReady(dataReady), 
	.data_o(oDATA)
);
always
  #10 clk_50M = ~clk_50M;
  
assign key[0] = reset_n;
assign key[1] = reset_n;
assign key[2] = reset_n;
assign key[3] = reset_n;

initial
  begin
  reset_n = 0;  
  clk_50M = 0 ;
  ir = 0;
  
  #30 reset_n = 1;
  
  // ir random noise
  #1_000_000;
  ir = 1;
  #20_000_000;
  ir = 0;
  #1_000_000;
  ir = 1;
  #3_000_000;

  // ir packet 
  #20_000_000;
  
  send_ir_leader();  
  send_ir_byte(8'h15);   
  send_ir_byte(8'h99);   
  send_ir_byte(8'h31);   
  send_ir_byte(8'hCE);   
  send_ir_end();       
  
  #20_000_000;
  
  send_ir_leader();  
  send_ir_byte(8'h12);  
  send_ir_byte(8'h34);   
  send_ir_byte(8'h56);   
  send_ir_byte(8'hA9);   
  send_ir_end();       
									//01001000 00101100 01101010 10010101
  #20_000_000;
  
  send_ir_leader();  
  send_ir_byte(8'h85);   
  send_ir_byte(8'h47);   
  send_ir_byte(8'hF0);   
  send_ir_byte(8'h0F);   
  send_ir_end();       
  
  #20_000_000;
  
  $stop;
  end
  
initial
  begin
  $monitor("time=%3d reset_n=%d oDATA=%x",$time,reset_n,oDATA);
  end
  
task send_ir_leader;  
 begin
  ir = 0;
  #9_000_000; // 9ms Lo
  ir = 1;
  #4_500_000; // 4.5ms Hi
 end
endtask 

task send_ir_byte;
  input [7:0] byte;
  integer i;
  begin
  $display("send ir = %x ",byte);
  for (i=0; i<8; i=i+1) 
     begin 
     ir = 0;
     #(DATA_LO_PERIOD);
     ir = 1;
     if(byte[0])
       #(DATA1_HI_PERIOD);
     else
       #(DATA0_HI_PERIOD); 
     byte = byte >> 1;       
     end      
  end   
endtask 

task send_ir_end;  
 begin
  ir = 0;
  #(DATA_LO_PERIOD);  
  ir = 1;  
 end
endtask
endmodule
```

## Wave

![IR_RX%20improve%204fa923245a4448a68b499af2a9556b92/Untitled.png](IR_RX%20improve%204fa923245a4448a68b499af2a9556b92/Untitled.png)

## Monitor

![IR_RX%20improve%204fa923245a4448a68b499af2a9556b92/Untitled%201.png](IR_RX%20improve%204fa923245a4448a68b499af2a9556b92/Untitled%201.png)

## Compilation Report

![IR_RX%20improve%204fa923245a4448a68b499af2a9556b92/Untitled%202.png](IR_RX%20improve%204fa923245a4448a68b499af2a9556b92/Untitled%202.png)

## RTL Viewer

![IR_RX%20improve%204fa923245a4448a68b499af2a9556b92/Untitled%203.png](IR_RX%20improve%204fa923245a4448a68b499af2a9556b92/Untitled%203.png)

## State Machine

![IR_RX%2086c99159ccd9424c988b6f4ef7a2577a/Untitled%203.png](IR_RX%2086c99159ccd9424c988b6f4ef7a2577a/Untitled%203.png)