# I2C_write_R_W(同步式設計)(100kHZ)

## I2C_write_R_W

```verilog
	module I2C_write_R_W(clk_sys, rst_n, en, data_R, data_W, ACK, ACK1, ACK2, ACK3, rstACK, SCLK, SDA, ldnACK1, ldnACK2, ldnACK3, R_W);
	/*---------ports declaration---------*/
	input       clk_sys, rst_n, en, R_W;
	input       [26:0]data_R;//read  27bit
	input       [17:0]data_W;//write 18bit
	output reg  ACK1, ACK2, ACK3;
	output wire ACK, rstACK, SCLK, ldnACK1, ldnACK2, ldnACK3;
	inout       SDA;
	/*---------variables---------*/
	reg  [4:0] count;
	reg  [8:0] cnt_I2C;
	reg        tick_I2C;
	reg        SCLK_100k;
	reg  [26:0]regdata_R;
	reg  [17:0]regdata_W;
	wire       SCLK_temp, SHEN, LDEN, SDO, countEN, rstcount;
	wire       SEL;
	/*---------assign wire---------*/
	//assign SEL = (SHEN)?(regdata[26]):(SDO);
	assign SEL = (SHEN)?((!R_W)?(regdata_W[17]):(regdata_R[26])):(SDO);
	assign SDA = (SEL)?(1'bz):(1'b0);
	assign ACK = ACK1|ACK2|ACK3;
	/*---------module instantiate---------*/
	I2C_control_R_W U0(
		.clk_sys(clk_sys),
		.SCLK_100k(SCLK_100k),
		.tick_I2C(tick_I2C),
		.rst_n(rst_n),
		.en(en),
		.count(count),
		.countEN(countEN),
		.rstcount(rstcount),
		.ACK1(ldnACK1),
		.ACK2(ldnACK2),
		.ACK3(ldnACK3),
		.rstACK(rstACK),
		.SCLK(SCLK),
		.SCLK_temp(SCLK_temp),
		.SHEN(SHEN),
		.LDEN(LDEN),
		.SDO(SDO),
		.R_W(R_W)
	);
	/*---------100k counter---------*/
	always@(posedge clk_sys or negedge rst_n)begin
		if(!rst_n)cnt_I2C <= 9'd0;
		else begin
			if(cnt_I2C<9'd499)cnt_I2C <= cnt_I2C + 9'd1;//0-499
			else              cnt_I2C <= 9'd0;
		end
	end
	/*---------I2C tick---------*/
	always@(posedge clk_sys or negedge rst_n)begin
		if(!rst_n)tick_I2C <= 1'b0;
		else begin
			if(cnt_I2C==9'd498)tick_I2C <= 1'b1;
			else               tick_I2C <= 1'b0;
		end
	end
	/*---------SCLK_100k---------*/
	always@(posedge clk_sys or negedge rst_n)begin
		if(!rst_n)SCLK_100k <= 1'b0;
		else begin
			if(cnt_I2C==9'd249)     SCLK_100k <= 1'b0;
			else if(cnt_I2C==9'd499)SCLK_100k <= 1'b1;
			else                    SCLK_100k <= SCLK_100k;
		end
	end
	/*---------counter---------*/
	always@(posedge clk_sys or negedge rst_n)begin
		if(!rst_n)begin
			count <= 5'd0;
		end
		else begin
			if(tick_I2C)begin
				if(rstcount)    count <= 5'd0;
				else if(countEN)count <= count + 5'd1;
				else            count <= count;
			end
			else count <= count;
		end
	end
	/*---------load data---------*/
	always@(posedge clk_sys or negedge rst_n)begin
		if(!rst_n)begin
			regdata_R <= 27'd0;
			regdata_W <= 18'd0;
		end
		else begin
			if(!R_W)begin
				if(tick_I2C)begin
					if(LDEN)     regdata_W <= data_W;
					else if(SHEN)regdata_W <= {regdata_W[16:0],1'b0};
					else         regdata_W <= regdata_W;
				end
				else regdata_W <= regdata_W;
			end 
			else begin
				if(tick_I2C)begin
					if(LDEN)     regdata_R <= data_R;
					else if(SHEN)regdata_R <= {regdata_R[25:0],1'b0};
					else         regdata_R <= regdata_R;
				end
				else regdata_R <= regdata_R;
			end
		end
	end
	/*---------ACK---------*/
	always@(posedge clk_sys or negedge rst_n)begin
		if(!rst_n)begin
			ACK1 <= 1'b0;
			ACK2 <= 1'b0;
			ACK3 <= 1'b0;
		end
		else if(rstACK&&tick_I2C)begin
			ACK1 <= 1'b0;
			ACK2 <= 1'b0;
			ACK3 <= 1'b0;
		end
		else begin
			if(ldnACK1&&tick_I2C)ACK1 <= SDA;
			else                 ACK1 <= ACK1;
			if(ldnACK2&&tick_I2C)ACK2 <= SDA;
			else                 ACK2 <= ACK2;
			if(ldnACK3&&tick_I2C)ACK3 <= SDA;
			else                 ACK3 <= ACK3;
		end
	end
	endmodule
```

## TestBench

```verilog
	`include "edge_detect.v"
	`timescale 1ns/1ns
	module tb_I2C_write_R_W();
	parameter data1 = 2'd0;
	parameter data2 = 2'd1;
	parameter data3 = 2'd2;
	parameter data4 = 2'd3;
	reg  clk_sys, rst_n, en, R_W;
	reg  [26:0]data_R;
	reg  [17:0]data_W;
	wire ACK1, ACK2, ACK3;
	wire ACK, rstACK, SCLK, ldnACK1, ldnACK2, ldnACK3;
	wire SDA1, SDA2;
	reg  regsda, flag;
	reg  [4:0]count;
	reg  [1:0]tb_state;
	wire pos_edge;
	assign SDA1 = (flag)?(regsda):(SDA2);
	integer i;
	I2C_write_R_W UUT(
		.clk_sys(clk_sys),
		.rst_n(rst_n),
		.en(en),
		.data_R(data_R), 
		.data_W(data_W),
		.ACK(ACK),
		.ACK1(ACK1),
		.ACK2(ACK2),
		.ACK3(ACK3),
		.rstACK(rstACK),
		.SCLK(SCLK),
		.SDA(SDA1), 
		.ldnACK1(ldnACK1),
		.ldnACK2(ldnACK2),
		.ldnACK3(ldnACK3),
		.R_W(R_W)
	);
	initial begin
		i        = 0; 
		regsda   = 1'b0;
		flag     = 1'b0;
		tb_state = 2'd0;
		count    = 5'd0;
		clk_sys  = 1'b0;
		data_W   = {8'h34, 1'b1, 8'h96, 1'b1};
		//data_R  = 27'b10101010_1_10101010_1_10101010_1;
		data_R   = 27'd0;
		R_W      = 1'b0;
	end
	always #50 clk_sys = ~clk_sys;
	initial begin
		rst_n = 0;
		while(1)
		#100 rst_n = 1;
	end
	initial begin
		en = 0;
		#100 en = 1;
		#100 en = 0;
	end
	always@(negedge SCLK)begin
		if(!R_W)begin
			if(count==5'd18)count <= 5'd0;
			else            count <= count + 5'd1;
		end
		else begin
			if(count==5'd27)count <= 5'd0;
			else            count <= count + 5'd1;
		end
		if(count==5'd8)begin
			flag   <= 1'b1;
			regsda <= 1'b0;
		end
		else if(count==5'd17)begin
			flag   <= 1'b1;
			regsda <= 1'b0;
		end
		else if(count==5'd26)begin
			flag   <= 1'b1;
			regsda <= 1'b0;
		end
		else begin
			flag   <= 1'b0;
			regsda <= 1'b1;
		end 
	end
	initial begin
		$monitor("time=%d, SDA=%d, SCLK=%d, count=%d",$time,SDA1,SCLK,count);
	end
	/*
	initial begin
		#2800 R_W = 1'b1;
		#100  data_R = {8'h55, 1'b1, 8'h62, 1'b1, 8'h23, 1'b1};
		      data_W = 18'd0;
		#100  en = 1;
		#100  en = 0;
	end
	initial begin
		#10000 $stop;
	end*/
	always@(negedge clk_sys)begin
		case(tb_state)
			data1:begin
				if(pos_edge)tb_state <= data2;
				else        tb_state <= data1;
			end
			data2:begin
				if(pos_edge)tb_state <= data3;
				else        tb_state <= data2;
			end
			data3:begin
				if(pos_edge)tb_state <= data4;
				else        tb_state <= data3;
			end
			data4:begin
				tb_state <= data4;
			end
		endcase
	end
	always@(posedge clk_sys)begin
		case(tb_state)
			data1:begin
				R_W      = 1'b0;
				data_W   = {8'h34, 1'b1, 8'h96, 1'b1};
				data_R   = 27'd0;
			end 
			data2:begin
				R_W      = 1'b1;
				data_W   = 18'd0;
				data_R   = {8'h99, 1'b1, 8'h28, 1'b1, 8'h13, 1'b1};
			end
			data3:begin
				R_W      = 1'b0;
				data_W   = {8'h61, 1'b1, 8'h89, 1'b1};
				data_R   = 27'd0;
			end
			data4:begin
				R_W      = 1'b1;
				data_W   = 18'd0;
				data_R   = {8'h77, 1'b1, 8'h15, 1'b1, 8'h36, 1'b1};
				i = i + 1;
			end
		endcase
	end
	always@(posedge clk_sys)begin
		if(pos_edge&&(i<=1))en = 1;
		else                en = 0;
	end
	edge_detect U0(
		.clk(clk_sys),
		.rst_n(rst_n),
		.data_in(rstACK),
		.pos_edge(pos_edge),
		.neg_edge()
   );
	initial begin
		#6500000 $finish;
	end
	endmodule
```

## Wave

![I2C_write_R_W(%E5%90%8C%E6%AD%A5%E5%BC%8F%E8%A8%AD%E8%A8%88)(100kHZ)%20f71c03a880504732a256b3e7d27e691c/Untitled.png](I2C_write_R_W(%E5%90%8C%E6%AD%A5%E5%BC%8F%E8%A8%AD%E8%A8%88)(100kHZ)%20f71c03a880504732a256b3e7d27e691c/Untitled.png)

![I2C_write_R_W(%E5%90%8C%E6%AD%A5%E5%BC%8F%E8%A8%AD%E8%A8%88)(100kHZ)%20f71c03a880504732a256b3e7d27e691c/Untitled%201.png](I2C_write_R_W(%E5%90%8C%E6%AD%A5%E5%BC%8F%E8%A8%AD%E8%A8%88)(100kHZ)%20f71c03a880504732a256b3e7d27e691c/Untitled%201.png)

## Compitation Repot

![I2C_write_R_W(%E5%90%8C%E6%AD%A5%E5%BC%8F%E8%A8%AD%E8%A8%88)(100kHZ)%20f71c03a880504732a256b3e7d27e691c/Untitled%202.png](I2C_write_R_W(%E5%90%8C%E6%AD%A5%E5%BC%8F%E8%A8%AD%E8%A8%88)(100kHZ)%20f71c03a880504732a256b3e7d27e691c/Untitled%202.png)

## RTL Viewer

![I2C_write_R_W(%E5%90%8C%E6%AD%A5%E5%BC%8F%E8%A8%AD%E8%A8%88)(100kHZ)%20f71c03a880504732a256b3e7d27e691c/Untitled%203.png](I2C_write_R_W(%E5%90%8C%E6%AD%A5%E5%BC%8F%E8%A8%AD%E8%A8%88)(100kHZ)%20f71c03a880504732a256b3e7d27e691c/Untitled%203.png)

## State Machine

![I2C_write_R_W(%E5%90%8C%E6%AD%A5%E5%BC%8F%E8%A8%AD%E8%A8%88)(100kHZ)%20f71c03a880504732a256b3e7d27e691c/Untitled%204.png](I2C_write_R_W(%E5%90%8C%E6%AD%A5%E5%BC%8F%E8%A8%AD%E8%A8%88)(100kHZ)%20f71c03a880504732a256b3e7d27e691c/Untitled%204.png)