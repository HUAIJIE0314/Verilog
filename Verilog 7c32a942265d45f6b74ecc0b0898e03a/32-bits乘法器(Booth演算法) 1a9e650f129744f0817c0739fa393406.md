# 32-bits乘法器(Booth演算法)

被乘數n位要移位n次

```verilog
module booth_multiplier(
  clkSys, 
  rst_n, 
  en, 
  a, 
  b, 
  product, 
  done
);
/*---------parameter----------*/
localparam IDLE   = 2'd0;
localparam CAL    = 2'd1;
localparam SHIFT  = 2'd2;
localparam FINISH = 2'd3;
/*---------ports declarations----------*/
input         clkSys; 
input         rst_n; 
input         en; 
input  [31:0] a; 
input  [31:0] b;
output [63:0] product; 
output        done;
reg    [63:0] product; 
reg           done;
/*---------variables----------*/
reg    [31:0] aReg;  //stored a
reg    [31:0] sReg;  //negative of a
reg    [64:0] pReg;  //product register
reg    [1:0]  fstate;//state register
reg    [5:0]  times; //count to width of a, b = 32
/*---------state----------*/
always@(posedge clkSys or negedge rst_n)begin
  if(!rst_n)fstate <= IDLE;
  else begin
    case(fstate)
	    IDLE:begin
		    if(en)fstate <= CAL;
        else  fstate <= IDLE;
      end
      CAL:begin
        if(times == 6'd32)fstate <= FINISH;
        else              fstate <= SHIFT;
      end
      SHIFT:  fstate <= CAL;
      FINISH: fstate <= IDLE;
      default:fstate <= IDLE;
    endcase
  end
end
/*---------output----------*/
always@(posedge clkSys or negedge rst_n)begin
  if(!rst_n)begin
    aReg    <= 32'd0;
    sReg    <= 32'd0;
    pReg    <= 65'd0;
    times   <= 6'd0;
    done    <= 1'b0;
    product <= 64'd0;
  end
  else begin
    case(fstate)
	   IDLE:begin
        aReg  <= a;
        sReg  <= (~a + 32'd1);
        pReg  <= {32'd0, b, 1'b0};
        done  <= 1'b0;
        times <= 6'd0;
      end
      CAL:begin
        if(pReg[1:0] == 2'b01 && times != 6'd32)     pReg <= {pReg[64:33]+aReg ,pReg[32:0]};//20 bit of MSB add aReg
        else if(pReg[1:0] == 2'b10 && times != 6'd32)pReg <= {pReg[64:33]+sReg ,pReg[32:0]};//20 bit of MSB minus aReg
        else                                         pReg <= pReg;
      end
      SHIFT:begin
        pReg  <= {pReg[64],pReg[64:1]};//signed shift
        times <= times + 6'd1;
      end
      FINISH:begin
        done    <= 1'b1;
        product <= pReg[64:1];
      end
      default:begin
        aReg    <= 32'd0;
        sReg    <= 32'd0;
	      pReg    <= 65'd0;
        times   <= 6'd0;
        done    <= 1'b0;
        product <= 64'd0;
      end
    endcase
  end
end
endmodule
```

## TestBench

```verilog
`timescale 10ns/10ns
module tb_booth();
reg                clk;
reg                rst_n;
reg  signed [31:0] a; 
reg  signed [31:0] b;
reg                en;
wire               done_sig;
wire signed [63:0] product;

booth_multiplier UUT(
  .clkSys(clk), 
  .rst_n(rst_n), 
  .en(en), 
  .a(a), 
  .b(b), 
  .product(product), 
  .done(done_sig)
);
initial begin
  clk = 0;
  rst_n = 0;
  a = 0;
  b = 0;
  en = 0;
  repeat(5)@(posedge clk)rst_n = 0;
  #10 rst_n = 1;
  test(32'd1, -32'd1);
  test(-32'd2, -32'd2);
  test(32'd3, -32'd3);
  test(32'd4, -32'd4);
  test(32'd5, -32'd5);
  test(32'd6, 32'd6);
  test(-32'd7, -32'd7);
  test(32'd8, -32'd8);
  test(32'd9, -32'd9);
  test(32'd10,-32'd10);
  
  test(32'd101, -32'd110);
  test(-32'd299, -32'd289);
  test(32'd330, -32'd3);
  test(32'd4986, -32'd4452);
  test(32'd5243, -32'd575);
  test(32'd2, 32'd67896);
  test(-32'd7, -32'd1334);
  test(32'd812, -32'd802);
  test(32'd0, -32'd9);
  test(32'd1000,-32'd123);
  
  #1000 $stop;
end
always #10 clk = ~clk;

task test;
  input  signed [31:0] in1;
  input  signed [31:0] in2;
  begin
    a = in1;
    b = in2;
	 #40 en = 1;
	 #20 en = 0;
    wait (done_sig == 1);
  end
endtask

endmodule
```

## Wave

![32-bits%E4%B9%98%E6%B3%95%E5%99%A8(Booth%E6%BC%94%E7%AE%97%E6%B3%95)%201a9e650f129744f0817c0739fa393406/Untitled.png](32-bits%E4%B9%98%E6%B3%95%E5%99%A8(Booth%E6%BC%94%E7%AE%97%E6%B3%95)%201a9e650f129744f0817c0739fa393406/Untitled.png)

## Compilation Report

![32-bits%E4%B9%98%E6%B3%95%E5%99%A8(Booth%E6%BC%94%E7%AE%97%E6%B3%95)%201a9e650f129744f0817c0739fa393406/Untitled%201.png](32-bits%E4%B9%98%E6%B3%95%E5%99%A8(Booth%E6%BC%94%E7%AE%97%E6%B3%95)%201a9e650f129744f0817c0739fa393406/Untitled%201.png)

## State Machine

![32-bits%E4%B9%98%E6%B3%95%E5%99%A8(Booth%E6%BC%94%E7%AE%97%E6%B3%95)%201a9e650f129744f0817c0739fa393406/Untitled%202.png](32-bits%E4%B9%98%E6%B3%95%E5%99%A8(Booth%E6%BC%94%E7%AE%97%E6%B3%95)%201a9e650f129744f0817c0739fa393406/Untitled%202.png)