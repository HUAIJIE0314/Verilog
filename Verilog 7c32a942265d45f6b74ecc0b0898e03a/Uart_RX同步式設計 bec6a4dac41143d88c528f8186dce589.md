# Uart_RX同步式設計

## Uart_RX

```verilog
	module Uart_RX(clk_sys, rst_n, RX_D, busy, dout, samp, data, pos, finish);
	/*----------parameter---------*/
	parameter IDLE     = 2'd0;
	parameter waitting = 2'd1;
	parameter data_RX  = 2'd2;
	parameter stop     = 2'd3;
	/*--------input/output--------*/
	input       clk_sys, rst_n, RX_D;
	output wire busy;
	output reg  [7:0]dout;
	output reg  samp, finish;
	/*----------variables---------*/
	reg        [1:0]fstate;
	output reg [2:0]pos;
	output reg [7:0]data;
	reg        [12:0]cnt;
	reg        counterEN;
	wire       tick_samp, tick_uart;
	/*----------------------------*/
	assign busy = (fstate != IDLE);
	assign tick_samp = (cnt==2602)?(1'b1):(1'b0);
	assign tick_uart = (cnt==5206)?(1'b1):(1'b0);
	/*----------9600HZ------------*/
	always@(posedge clk_sys or negedge rst_n)begin
		if(!rst_n)cnt <= 13'd0;
		else begin
			if(counterEN)begin
				if(cnt < 13'd5207)cnt <= cnt + 13'd1;
				else cnt <= 13'd0;
			end
			else begin
				cnt <= 13'd0;
			end
		end
	end
	/*-----------fstate------------*/
	always@(posedge clk_sys or negedge rst_n)begin
		if(!rst_n)fstate <= IDLE;
		else begin
			case(fstate)
				IDLE:begin
					if(!RX_D)fstate <= waitting;
					else     fstate <= IDLE;
				end
				waitting:begin
					if(tick_uart==1'b1)fstate <= data_RX;
					else               fstate <= waitting;
				end
				data_RX:begin
					if(pos==3'd7&&tick_uart==1'b1)fstate <= stop;
					else                          fstate <= data_RX;
				end
				stop:begin
					if(tick_uart==1'b1)fstate <= IDLE;
					else               fstate <= stop;
				end
				default:fstate <= IDLE;//10000000
			endcase
		end
	end
	/*-----------output-----------*/
	always@(posedge clk_sys or negedge rst_n)begin
		if(!rst_n)begin
			data <= 8'd0;
			pos  <= 3'd0;
			dout <= 8'd0;
			samp <= 1'b0;
			counterEN <= 1'b0;
		end
		else begin
			case(fstate)
				IDLE:begin
					counterEN <= 1'b0;
					data <= data;
					pos  <= 3'd0;
					samp <= 1'b0;
				end
				waitting:begin
					counterEN <= 1'b1;
					data <= data;
					pos  <= 3'd0;
					samp <= 1'b0;
				end
				data_RX:begin
					counterEN <= 1'b1;
					if(tick_uart==1'b1)begin
						if(pos<3'd7)data <= data>>1;
						else			  data <= data;
						pos              <= pos + 3'd1;
						samp             <= 1'b0;
					end
					else if(tick_samp==1'b1)begin
						data[7] <= RX_D;
						pos     <= pos;
						samp    <= 1'b1;
					end
					else begin
						data <= data;
						pos  <= pos;
						samp <= 1'b0;
					end
				end
				stop:begin
					counterEN <= 1'b1;
					pos  <= 3'd0;
					samp <= 1'b0;
					dout <= data;
				end
				default:begin
					counterEN <= 1'b0;
					dout      <= 8'd0;
					data      <= 8'd0;
					samp      <= 1'b0;
					pos       <= 3'd0;
				end 
			endcase
		end
	end
	always@(posedge clk_sys or negedge rst_n)begin
		if(!rst_n)finish <= 1'b0;
		else begin
			if(fstate==stop&&tick_uart)finish <= 1'b1;
			else                       finish <= 1'b0;
		end
	end
	endmodule
```

## TestBench

```verilog
	`timescale 1ns/1ns
	module tb_Uart_RX();
	parameter BIT_PERIOD      = 104166; // (1/9600)*1000000000
	reg clk_sys, rst_n, RX_D;
	wire busy;
	wire [7:0]dout;
	wire samp;
	wire finish;
	integer i = 0, j = 0;
	reg [14:0]tx_data = 10'd0;
	wire [2:0]pos;
	reg [7:0]ADD = 8'h41;
	wire [7:0] data;
	Uart_RX U0(
		.clk_sys(clk_sys), 
		.rst_n(rst_n), 
		.RX_D(RX_D), 
		.busy(busy), 
		.dout(dout),
		.samp(samp),
		.data(data),
		.pos(pos),
		.finish(finish)
	);
	/*----------9600HZ------------*/
	
	always begin
		#10 clk_sys <= ~clk_sys;
 	end
	
	initial begin
		clk_sys <= 1'b0;
		rst_n     <= 1'b0;
		RX_D    <= 1'b1;
		repeat(2)@(posedge clk_sys)rst_n <= 1'b0;
		rst_n <= 1'b1; 
	end
	
	initial begin
		#100;
		for(j=0;j<15;j = j + 1)begin
			tx_data <= {{6{1'b1}}, ADD, 1'b0};
			for(i=0;i<15;i = i + 1)begin
				#BIT_PERIOD RX_D <= tx_data[i];
			end
			ADD <= ADD + 8'h01;
		end
	end
	endmodule
```

## Wave

![Uart_RX%E5%90%8C%E6%AD%A5%E5%BC%8F%E8%A8%AD%E8%A8%88%20bec6a4dac41143d88c528f8186dce589/Untitled.png](Uart_RX%E5%90%8C%E6%AD%A5%E5%BC%8F%E8%A8%AD%E8%A8%88%20bec6a4dac41143d88c528f8186dce589/Untitled.png)

## Compiltion Raport

![Uart_RX%E5%90%8C%E6%AD%A5%E5%BC%8F%E8%A8%AD%E8%A8%88%20bec6a4dac41143d88c528f8186dce589/Untitled%201.png](Uart_RX%E5%90%8C%E6%AD%A5%E5%BC%8F%E8%A8%AD%E8%A8%88%20bec6a4dac41143d88c528f8186dce589/Untitled%201.png)

## RTL Viewer

![Uart_RX%E5%90%8C%E6%AD%A5%E5%BC%8F%E8%A8%AD%E8%A8%88%20bec6a4dac41143d88c528f8186dce589/Untitled%202.png](Uart_RX%E5%90%8C%E6%AD%A5%E5%BC%8F%E8%A8%AD%E8%A8%88%20bec6a4dac41143d88c528f8186dce589/Untitled%202.png)

## State Machine

![Uart_RX%E5%90%8C%E6%AD%A5%E5%BC%8F%E8%A8%AD%E8%A8%88%20bec6a4dac41143d88c528f8186dce589/Untitled%203.png](Uart_RX%E5%90%8C%E6%AD%A5%E5%BC%8F%E8%A8%AD%E8%A8%88%20bec6a4dac41143d88c528f8186dce589/Untitled%203.png)