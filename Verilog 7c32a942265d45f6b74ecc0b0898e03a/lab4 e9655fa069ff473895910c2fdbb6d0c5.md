# lab4

## Lab4

```verilog
module lab4(
		CLOCK_50,
		RST_n,  
		KEY_SCAN,
		KEY_READ,
		Key_OUT,
		IRDA_TXD                   
   ); 
	/*---------Ports Declarations---------*/
	input        CLOCK_50;
	input        RST_n;
	input   [3:0]KEY_READ;
	output  [3:0]KEY_SCAN;
	output  [3:0]Key_OUT;
	output       IRDA_TXD;
	/*---------variables---------*/
	//keyScan
	wire        high2;
	//lab4 module
	wire        sfr_wr;
	wire	      sfr_rd; 
	wire	 [7:0]sfr_addr; 
	wire	 [7:0]sfr_data_out; 
	reg 	 [7:0]sfr_data_in;
	reg    [7:0]dataF2;
	reg    [7:0]dataF3;
	reg    [7:0]dataF4;
	reg    [7:0]dataF5;
	reg    [7:0]ebReg;
	reg    [7:0]ecReg;
	reg         flag;
	reg    [2:0]dly;
	//IR_TX
	wire        sentfinish;
	wire        en;
	reg         enTemp;
	//8051 module
	wire        por_n;
	wire        rst_out_n;
	wire        test_mode_n;
	wire        stop_mode_n;
	wire        idle_mode_n;
	wire  [15:0]mem_addr; 
   wire   [7:0]mem_data_out; 
   wire   [7:0]mem_data_in; 
   wire        mem_wr_n; 
   wire        mem_rd_n; 
   wire        mem_pswr_n;    
   wire        mem_psrd_n; 
   wire        mem_ale; 
   wire        mem_ea_n;// 1: external ROM ,0: Internal ROM
   wire        int0_n; 
   wire        int1_n; 
   wire        int2; 
   wire        int3_n; 
   wire        int4; 
   wire        int5_n; 
   wire        pfi; 
   wire        wdti; 
   wire        rxd0_in; 
   wire        rxd0_out, txd0; 
   wire        rxd1_in; 
   wire        rxd1_out, txd1; 
   wire        t0; 
   wire        t1; 
   wire        t2; 
   wire        t2ex; 
   wire        t0_out, t1_out, t2_out; 
   wire        port_pin_reg_n, p0_mem_reg_n, p0_addr_data_n, p2_mem_reg_n; 
   wire   [7:0]iram_addr, iram_data_out, iram_data_in; 
   wire        iram_rd_n, iram_we1_n, iram_we2_n; 
   wire  [15:0]irom_addr; 
   wire   [7:0]irom_data_out; 
   wire        irom_rd_n, irom_cs_n;
	/*---------8051_core module instantiation---------*/	
	DW8051_core U0(
		.clk(CLOCK_50),
		.por_n(RST_n),
		.rst_in_n(RST_n),
		.rst_out_n(rst_out_n),
		.test_mode_n(test_mode_n),
		.stop_mode_n(stop_mode_n),
		.idle_mode_n(idle_mode_n),
		.sfr_addr(sfr_addr),//in
		.sfr_data_out(sfr_data_out),//out
		.sfr_data_in(sfr_data_in),//
		.sfr_wr(sfr_wr),//
		.sfr_rd(sfr_rd),//
		.mem_addr(mem_addr),
		.mem_data_out(mem_data_out),
		.mem_data_in(mem_data_in),
		.mem_wr_n(mem_wr_n),
		.mem_rd_n(mem_rd_n),
		.mem_pswr_n(mem_pswr_n),
		.mem_psrd_n(mem_psrd_n),
		.mem_ale(mem_ale),
		.mem_ea_n(mem_ea_n),
		.int0_n(int0_n),
		.int1_n(int1_n),
		.int2(int2),
		.int3_n(int3_n),
		.int4(int4),
		.int5_n(int5_n),
		.pfi(pfi),
		.wdti(wdti),
		.rxd0_in(rxd0_in),
		.rxd0_out(rxd0_out),
		.txd0(txd0),
		.rxd1_in(rxd1_in),
		.rxd1_out(rxd1_out),
		.txd1(txd1),
		.t0(t0),
		.t1(t1),
		.t2(t2),
		.t2ex(t2ex),
		.t0_out(t0_out),
		.t1_out(t1_out),
		.t2_out(t2_out),
		.port_pin_reg_n(port_pin_reg_n),
		.p0_mem_reg_n(p0_mem_reg_n),
		.p0_addr_data_n(p0_addr_data_n),
		.p2_mem_reg_n(p2_mem_reg_n),
		.iram_addr(iram_addr),
		.iram_data_out(iram_data_out),
		.iram_data_in(iram_data_in),
		.iram_rd_n(),
		.iram_we1_n(iram_we1_n),
		.iram_we2_n(iram_we2_n),
		.irom_addr(irom_addr),
		.irom_data_out(irom_data_out),
		.irom_rd_n(irom_rd_n),
		.irom_cs_n(irom_cs_n)
	);
	assign mem_ea_n = 1'b1;
	
	int_mem u1_int_mem(
		.clk(CLOCK_50),
		.addr(iram_addr),
		.data_in(iram_data_in),
		.data_out(iram_data_out),
		.we1_n(iram_we1_n),
		.we2_n(iram_we2_n),
		.rd_n(iram_rd_n)
	);
	//ip
	/*
	ram	ram_inst (
	.address ( iram_addr ),
	.clock ( ~CLOCK_50 ),
	.data ( iram_data_in ),
	.wren ( ~iram_we2_n ),
	.q ( iram_data_out )
	);
	*/
	
	/*                                  
   ext_mem u2_ext_mem(
		.addr(mem_addr),
		.data_in(mem_data_out),
		.data_out(mem_data_in),
		.wr_n(mem_wr_n),
		.rd_n(mem_rd_n)
	);  */   
		
   rom_mem u2_rom_mem(
		.addr(irom_addr),
		.data_out(irom_data_out),                    
		.rd_n(irom_rd_n),
		.cs_n(irom_cs_n)
	); 
	
	//ip
	/*
	rom	rom_inst (
	.address ( irom_addr ),
	.clken ( ~irom_rd_n ),
	.clock ( CLOCK_50 ),
	.q ( irom_data_out )
	);
	*/
	/*---------module IR_TX instantiation---------*/
	IR_TX U3(
		.clkSys(CLOCK_50),     //input  clkSys
		.en(en),             //input  en
		.rst_n(RST_n),         //input  rst_n
		.data_in({dataF2,dataF3,dataF4,dataF5}),//input  data_in
		.iRDA_TX(IRDA_TXD),    //output iRDA_TX
		.sentfinish(sentfinish)//output sentfinish
	);
	/*---------module keyScan instantiation---------*/
	keyScan U4(
		.clk(CLOCK_50), 
		.rst_n(RST_n), 
		.key_scan(KEY_SCAN),
		.key_read(KEY_READ), 
		.Key_out(Key_OUT),
		.high2(high2)
	);
	/*---------assign wire---------*/
	assign test_mode_n = 1'b1;
	assign en = |dly;
	/*---------en---------*/
	always@(posedge CLOCK_50 or negedge RST_n)begin
		if(!RST_n)begin
			enTemp <= 1'b0;
		end 
		else begin
			if(Key_OUT!=4'd0)enTemp <= high2;
			else             enTemp <= 1'b0;
		end
	end
	/*---------sfr_data_in---------*/
	always@(posedge CLOCK_50 or negedge RST_n)begin
		if(!RST_n)begin
			sfr_data_in <= 8'h00;
			dataF2      <= 8'h00;
			dataF3      <= 8'h00;
			dataF4      <= 8'h00;
			dataF5      <= 8'h00;
			flag        <= 1'b0;
		end 
		else begin
			if(Key_OUT!=4'd0 && !flag)begin
				sfr_data_in <= 8'h10;
				flag        <= 1'b1;
			end 
			else if(sfr_addr == 8'hea)sfr_data_in <= ebReg;
			else if(sfr_addr == 8'heb)sfr_data_in <= ecReg;
			else if(sfr_addr == 8'hec)sfr_data_in <= 8'h00;
			else if(sfr_addr == 8'hf2 && sfr_wr)dataF2 <= sfr_data_out;
			else if(sfr_addr == 8'hf3 && sfr_wr)dataF3 <= sfr_data_out;
			else if(sfr_addr == 8'hf4 && sfr_wr)dataF4 <= sfr_data_out;
			else if(sfr_addr == 8'hf5 && sfr_wr)dataF5 <= sfr_data_out;
			else if(sentfinish)begin
				sfr_data_in <= 8'h10;
				flag        <= 1'b0;
			end 
			//else if(sfr_addr == 8'hf1 && sfr_data_in == 8'h10)
		end
	end
	/*---------case EB, EC register---------*/
	always@(posedge CLOCK_50 or negedge RST_n)begin
		if(!RST_n)ebReg <= 8'h00;
		else begin
			case(Key_OUT)
				4'd0:ebReg <= 8'd1;
				4'd1:ebReg <= 8'd2;
				4'd2:ebReg <= 8'd4;
				4'd3:ebReg <= 8'd8;
				4'd4:ebReg <= 8'd16;
				4'd5:ebReg <= 8'd32;
				4'd6:ebReg <= 8'd64;//40
				4'd7:ebReg <= 8'd128;
				default:ebReg <= 8'h00;
			endcase
		end
	end
	always@(posedge CLOCK_50 or negedge RST_n)begin
		if(!RST_n)ecReg <= 8'h00;
		else begin
			case(Key_OUT)
				4'd8: ecReg <= 8'd1;
				4'd9: ecReg <= 8'd2;
				4'd10:ecReg <= 8'd4;
				4'd11:ecReg <= 8'd8;
				4'd12:ecReg <= 8'd16;
				4'd13:ecReg <= 8'd32;
				4'd14:ecReg <= 8'd64;
				4'd15:ecReg <= 8'd128;
				default:ecReg <= 8'h00;
			endcase
		end
	end
	/*---------shifting---------*/
	always@(posedge CLOCK_50 or  negedge RST_n)begin
		if(!RST_n)dly <= 3'd0;
		else dly <= {dly[1:0], enTemp};
	end
	endmodule
```

## IR_TX

```verilog
module IR_TX(
		clkSys,    //input  clkSys
		en,        //input  en
		rst_n,     //input  rst_n
		data_in,   //input  data_in
		iRDA_TX,   //output iRDA_TX
		sentfinish //output sentfinish
	);
	/*---------parameter Declarations---------*/
	//===== state machine =====//
	localparam IDLE     = 3'd0;
	localparam LEADER   = 3'd1;
	localparam DATALOAD = 3'd2;
	localparam DATASENT = 3'd3;
	localparam MAKETICK = 3'd4;
	localparam WAIT     = 3'd5;
	localparam FINISH   = 3'd6;
	localparam REPEAT   = 3'd7;
	//===== during time =====//
	//cnt_38k
	localparam durIRCLOCK  = 10'd658;    //38KHZ
	//cnt_repeat
	localparam durRepeat   = 23'd5500000;//110ms
	localparam RepeatHIGH  = 23'd450000; //9ms
	localparam RepeatLOW   = 23'd112500; //2.25ms
	localparam dur11250u   = 23'd562500; //11.25ms
	localparam dur13500u   = 23'd675000; //13.5ms
	//counter
	localparam durLeader   = 20'd675000; //13.5ms
	localparam LeaderLOW   = 20'd225000; //4.5ms
	localparam LeaderHIGH  = 20'd450000; //9ms
	localparam Data2250us  = 20'd112500; //2.25ms
	localparam Data1120us  = 20'd56000;  //1.12ms
	localparam Data1690us  = 20'd84500;  //3.932ms
	localparam Data560us   = 20'd28000;  //560us
	/*---------Ports Declarations---------*/
	input        clkSys;
	input        en;
	input        rst_n;
	input  [31:0]data_in;
	output       iRDA_TX;
	output       sentfinish;
	reg          sentfinish;
	wire         iRDA_TX;
	/*---------variables---------*/
	reg     [2:0]fstate;
	reg     [9:0]cnt_38k;
	reg    [22:0]cnt_repeat;
	reg    [19:0]counter;
	reg    [31:0]dataReg;
	reg     [4:0]index;
	reg          iRDAreg;
	reg          clk_38K;
	reg          counterEN;
	reg          cntrepeatEN;
	reg          LDEN;
	wire   [19:0]counterTemp;
	wire   [19:0]DataDur;
	wire   [31:0]mappingReg;
	/*---------assign wire---------*/
	assign iRDA_TX     = (iRDAreg)?(clk_38K):(1'b0);
	assign counterTemp = (dataReg[31])?(Data2250us):(Data1120us);
	assign mappingReg  = {data_in[7:0],data_in[15:8],data_in[23:16],data_in[31:24]};
	assign DataDur     = (dataReg[index])?(Data2250us):(Data1120us);
	/*---------counter---------*/
	always@(posedge clkSys or negedge rst_n)begin
		if(!rst_n)        counter <= 19'd0;
		else if(counterEN)counter <= counter + 19'd1;
		else              counter <= 19'd0;
	end
	/*---------cnt_38k---------*/
	always@(posedge clkSys or negedge rst_n)begin
		if(!rst_n)                   cnt_38k <= 10'd0;
		else if(cnt_38k < durIRCLOCK)cnt_38k <= cnt_38k + 10'd1;
		else                         cnt_38k <= 10'd0;
	end
	/*---------cnt_repeat---------*/
	always@(posedge clkSys or negedge rst_n)begin
		if(!rst_n)cnt_repeat <= 23'd0;
		else begin
			if(cntrepeatEN)begin
				if(cnt_repeat < durRepeat)cnt_repeat <= cnt_repeat + 23'd1;
				else                      cnt_repeat <= 23'd0;
			end
			else cnt_repeat <= 23'd0;
		end                        
	end
	/*---------clk_38K---------*/
	always@(posedge clkSys or negedge rst_n)begin
		if(!rst_n)                    clk_38K <= 1'b0;
		else if(cnt_38k == durIRCLOCK)clk_38K <= ~clk_38K;
		else                          clk_38K <= clk_38K;
	end
	/*---------fstate_state---------*/
	always@(posedge clkSys or negedge rst_n)begin
		if(!rst_n)fstate <= IDLE;
		else begin
			case(fstate)
				IDLE:begin
					if(en)fstate <= LEADER;
					else  fstate <= IDLE;
				end
				LEADER:begin
					if(counter == durLeader)fstate <= DATALOAD;
					else                    fstate <= LEADER;
				end
				DATALOAD:begin
					fstate <= DATASENT;
				end
				DATASENT:begin
					if(index == 5'd31 && counter == counterTemp)fstate <= MAKETICK;
					else                                        fstate <= DATASENT;
				end
				MAKETICK:begin
					if(counter == Data560us)fstate <= WAIT;
					else                    fstate <= MAKETICK;
				end
				WAIT:begin
					if(cnt_repeat == durRepeat)begin
						if(en)fstate <= REPEAT;
						else  fstate <= FINISH;
					end
					else     fstate <= WAIT;
				end
				FINISH:begin
					fstate <= IDLE;
				end
				REPEAT:begin
					if(!en)fstate <= FINISH;
					else   fstate <= REPEAT;
				end
				default: fstate <= IDLE;
			endcase
		end
	end
	/*---------fstate_output---------*/
	always@(posedge clkSys or negedge rst_n)begin
		if(!rst_n)begin
			counterEN   <= 1'b0;
			cntrepeatEN <= 1'b0;
			iRDAreg     <= 1'b0;
			LDEN        <= 1'b0;
			index       <= 5'd0;
			sentfinish  <= 1'b0;
		end 
		else begin
			case(fstate)
				IDLE:begin
					counterEN   <= 1'b0;
					cntrepeatEN <= 1'b0;
					iRDAreg     <= 1'b0;
					LDEN        <= 1'b0;
					index       <= 5'd0;
					sentfinish  <= 1'b0;
				end
				LEADER:begin
					if(counter < durLeader) counterEN <= 1'b1;
					else                    counterEN <= 1'b0;
					if(counter < LeaderHIGH)iRDAreg   <= 1'b1;
					else                    iRDAreg   <= 1'b0;
					cntrepeatEN <= 1'b1;
					LDEN        <= 1'b0;
					index       <= 5'd0;
					sentfinish  <= 1'b0;
				end
				DATALOAD:begin
					counterEN   <= 1'b0;
					cntrepeatEN <= 1'b1;
					iRDAreg     <= 1'b0;
					index       <= 5'd0;
					LDEN        <= 1'b1;
					sentfinish  <= 1'b0;
				end
				DATASENT:begin
					if(counter < (DataDur-20'd1))counterEN <= 1'b1;
					else if(counter == DataDur)index <= index + 5'd1;
					else counterEN <= 1'b0;
					if(counter < Data560us)iRDAreg   <= 1'b1;
					else                   iRDAreg   <= 1'b0;
					cntrepeatEN <= 1'b1;
					LDEN        <= 1'b0;
					sentfinish  <= 1'b0;
				end
				MAKETICK:begin
					if(counter < Data560us)begin
						counterEN <= 1'b1;
						iRDAreg   <= 1'b1;
					end 
					else begin
						counterEN <= 1'b0;
						iRDAreg   <= 1'b0;
					end 
					cntrepeatEN <= 1'b1;
					index       <= 5'd0;
					LDEN        <= 1'b0;
					sentfinish  <= 1'b0;
				end
				WAIT:begin
					counterEN <= 1'b0;
					iRDAreg   <= 1'b0; 
					cntrepeatEN <= 1'b1;
					index       <= 5'd0;
					LDEN        <= 1'b0;
					sentfinish  <= 1'b0;
				end
				FINISH:begin
					counterEN   <= 1'b0;
					cntrepeatEN <= 1'b0;
					iRDAreg     <= 1'b0;
					index       <= 5'd0;
					LDEN        <= 1'b0;
					sentfinish  <= 1'b1;
				end
				REPEAT:begin
					if((cnt_repeat < RepeatHIGH )|| (cnt_repeat > dur11250u && cnt_repeat < dur13500u))iRDAreg <= 1'b1;
					else                                                                               iRDAreg <= 1'b0;
					counterEN   <= 1'b0;
					cntrepeatEN <= 1'b1;
					LDEN        <= 1'b0;
					index       <= 5'd0;
					sentfinish  <= 1'b0;
				end
				default:begin
					counterEN   <= 1'b0;
					cntrepeatEN <= 1'b0;
					LDEN        <= 1'b0;
					index       <= 5'd0;
					iRDAreg     <= 1'b0;
					sentfinish  <= 1'b0;
				end
			endcase
		end
	end
	/*---------Load Data---------*/
	always@(posedge clkSys or negedge rst_n)begin
		if(!rst_n)   dataReg <= 32'd0;
		else if(LDEN)dataReg <= mappingReg;
		else         dataReg <= dataReg;
	end
	endmodule
```

## keyScan

```verilog
module keyScan(
		clk, 
		rst_n, 
		key_scan,
		key_read, 
		Key_out,
		high2
	);
	/*---------parameter---------*/
	localparam durms   = 16'd50000;
	localparam durbe   = 20'd750000;
	localparam durmiss = 20'd150000;
	/*---------Ports Declarations---------*/
	input        clk;
	input        rst_n;
	input   [3:0]key_read;
	output  [3:0]key_scan;
	output  [3:0]Key_out;
	output       high2;
	reg     [3:0]key_scan;
	reg     [3:0]Key_out;
	/*---------variables---------*/
	reg    [15:0]counter;
	reg    [19:0]counterDeb; 	
	reg     [1:0]cnt;
	reg          flag;
	reg          flag_add;
	reg          flag_en;
	reg     [3:0]readTemp;
	reg     [3:0]scanTemp;
	reg     [1:0]cntTemp;
	reg     [2:0]dly;
	reg     [3:0]dly2;
	reg          Dout;
	wire         low;
	wire         high;
	wire         high2;
	/*---------assign wire---------*/
	assign high2 = &dly2;
	assign high = &dly;
	assign low  = |dly;
	/*---------shifting---------*/
	always@(posedge clk or  negedge rst_n)begin
		if(!rst_n)dly <= 3'd0;
		else dly <= {dly[1:0], flag_add};
	end
	/*---------shifting2---------*/
	always@(posedge clk or  negedge rst_n)begin
		if(!rst_n)dly2 <= 4'd0;
		else dly2 <= {dly2[2:0], Dout};
	end
	/*---------Dout---------*/
	always@(posedge clk or negedge rst_n)begin
		if(!rst_n)Dout <= 1'b0;
		else begin
			if(low)       Dout <= low;
			else if(!high)Dout <= high;
			else          Dout <= Dout;
		end
	end
	/*---------1000HZ---------*/
	always@(posedge clk or negedge rst_n)begin
		if(!rst_n)begin
			counter <= 16'd0;
			cnt     <= 2'd0;
		end 
		else begin
			if(counter < durms)begin
				counter <= counter + 16'd1;
				cnt     <= cnt;
			end 
			else begin
				counter <= 16'd0;
				cnt     <= cnt + 2'd1;
			end 
		end
	end
	/*---------15ms---------*/
	always@(posedge clk or negedge rst_n)begin
		if(!rst_n)counterDeb <= 20'd0;
		else begin
			/*
			if(flag)         counterDeb <= counterDeb + 20'd1;
			else if(flag_add)counterDeb <= counterDeb + durmiss;
			else if(cntTemp == cnt)begin
				if(key_read != readTemp || key_scan != scanTemp)counterDeb <= 20'd0;
				else                                            counterDeb <= counterDeb;
			end
			else counterDeb <= counterDeb;
			*/
			if(high2)counterDeb <= counterDeb + 20'd1;
			else     counterDeb <= 20'd0;
		end
	end
	/*---------flag---------*/
	always@(posedge clk or negedge rst_n)begin
		if(!rst_n)begin
			readTemp <= 4'd0;
			scanTemp <= 4'd0;
			cntTemp  <= 2'd0;
			flag     <= 1'b0;
			flag_add <= 1'b0;
			flag_en  <= 1'b0;
		end 
		else begin
			if(!(key_read == 4'b0000) && !flag_en)begin
				readTemp <= key_read;
				scanTemp <= key_scan;
				cntTemp  <= cnt;
				flag     <= 1'b1;
				flag_add <= 1'b0;
				flag_en  <= 1'b1;
			end
			else if(cntTemp == cnt)begin
				if(key_read == readTemp && key_scan == scanTemp)begin
					flag_add <= 1'b1;
					readTemp <= key_read;
					scanTemp <= key_scan;
					cntTemp  <= cnt;
					flag_en  <= 1'b1;
				end
				else begin
					flag_add <= 1'b0;
					readTemp <= 4'd0;
					scanTemp <= 4'd0;
					cntTemp  <= 2'd0;
					flag_en  <= 1'b0;
				end
			end
			else begin
				readTemp <= key_read;
				scanTemp <= key_scan;
				flag     <= flag;
				flag_add <= flag_add;
				flag_en  <= flag_en;
			end
		end
	end
	/*---------Scan key_scan---------*/
	always@(posedge clk or negedge rst_n)begin
		if(!rst_n)key_scan <= 4'd0;
		else begin
			case(cnt)
				2'd0:key_scan <= 4'b0001;
				2'd1:key_scan <= 4'b0010;
				2'd2:key_scan <= 4'b0100;
				2'd3:key_scan <= 4'b1000;
				default:key_scan <= 4'b0001;
			endcase
		end
	end
	/*---------select Key_out---------*/
	always@(posedge clk or negedge rst_n)begin
		if(!rst_n)Key_out <= 4'd0;
		else begin
			if(counterDeb >= durbe && cnt == cntTemp)begin
			case(key_read)
				4'b0001:begin
					case(key_scan)
						4'b0001:Key_out <= 4'd0;
						4'b0010:Key_out <= 4'd4;
						4'b0100:Key_out <= 4'd8;
						4'b1000:Key_out <= 4'd12;
						default:Key_out <= 4'd0;
					endcase
				end
				4'b0010:begin
					case(key_scan)
						4'b0001:Key_out <= 4'd1;
						4'b0010:Key_out <= 4'd5;
						4'b0100:Key_out <= 4'd9;
						4'b1000:Key_out <= 4'd13;
						default:Key_out <= 4'd0;
					endcase
				end
				4'b0100:begin
					case(key_scan)
						4'b0001:Key_out <= 4'd2;
						4'b0010:Key_out <= 4'd6;
						4'b0100:Key_out <= 4'd10;
						4'b1000:Key_out <= 4'd14;
						default:Key_out <= 4'd0;
					endcase
				end
				4'b1000:begin
					case(key_scan)
						4'b0001:Key_out <= 4'd3;
						4'b0010:Key_out <= 4'd7;
						4'b0100:Key_out <= 4'd11;
						4'b1000:Key_out <= 4'd15;
						default:Key_out <= 4'd0;
					endcase
				end
				default:Key_out <= 4'd0;
			endcase
			end
			else Key_out <= Key_out;
		end
	end
	
	endmodule
```

## TestBench

```verilog
`timescale 1ns/1ns

module lab4_tb;

parameter DATA_HI_PERIOD   = 21; // 560us
parameter DATA0_LO_PERIOD  = 21; // 21*26.315=560us
parameter DATA1_LO_PERIOD  = 64; // 64*26.315=1684us

reg clk_50M;
reg reset_n;
wire [3:0] key;
reg ir;

wire [3:0] key_read;
wire [3:0] key_scan;
wire [3:0] key_out;

wire       ir_tx;
reg        ir_en;
reg        clk_38K;
reg  [15:0] ir_tx_enlop_cnt;
reg        ir_tx_enlop;
reg        ir_tx_d1;
reg        ir_tx_d2;
wire       ir_tx_pos;
reg        k0_ctrl;
reg        k1_ctrl;
reg        k2_ctrl;
reg        k3_ctrl;
reg        k4_ctrl;
reg        k5_ctrl;
reg        k6_ctrl;
reg        k7_ctrl;
reg        k8_ctrl;
reg        k9_ctrl;
reg        k10_ctrl;
reg        k11_ctrl;
reg        k12_ctrl;
reg        k13_ctrl;
reg        k14_ctrl;
reg        k15_ctrl;
wire       ir_tx_enlop_rev;

pulldown(key_read[0]);
pulldown(key_read[1]);
pulldown(key_read[2]);
pulldown(key_read[3]);

tranif1( key_read[0] , key_scan[0], k0_ctrl);
tranif1( key_read[1] , key_scan[0], k1_ctrl);
tranif1( key_read[2] , key_scan[0], k2_ctrl);
tranif1( key_read[3] , key_scan[0], k3_ctrl);
tranif1( key_read[0] , key_scan[1], k4_ctrl);
tranif1( key_read[1] , key_scan[1], k5_ctrl);
tranif1( key_read[2] , key_scan[1], k6_ctrl);
tranif1( key_read[3] , key_scan[1], k7_ctrl);
tranif1( key_read[0] , key_scan[2], k8_ctrl);
tranif1( key_read[1] , key_scan[2], k9_ctrl);
tranif1( key_read[2] , key_scan[2], k10_ctrl);
tranif1( key_read[3] , key_scan[2], k11_ctrl);
tranif1( key_read[0] , key_scan[3], k12_ctrl);
tranif1( key_read[1] , key_scan[3], k13_ctrl);
tranif1( key_read[2] , key_scan[3], k14_ctrl);
tranif1( key_read[3] , key_scan[3], k15_ctrl);

lab4 lab4(
        //////// CLOCK //////////
        .CLOCK_50(clk_50M),
        .RST_n(reset_n),
        //////// LCM //////////
        .KEY_SCAN(key_scan),
        .KEY_READ(key_read),
		  .Key_OUT(key_out),
        //////// IR Tx //////////
        .IRDA_TXD(ir_tx)                      
        );  

IR_RX IR_RX(
            .CLOCK_50(clk_50M), 
            .RSTN(reset_n), 
            .IRDA_RXD(ir_tx_enlop_rev), 
            .DATA()            
            );
always
  #10 clk_50M = ~clk_50M;

//always
//  #13157 clk_38K = ~clk_38K;   // 38KHz 
  
always@(posedge clk_50M)
  begin
  if(lab4_tb.IR_RX.state == 2'b11)
    $display("time=%3d IR data = %x,%x,%x,%x ", $time,lab4_tb.IR_RX.ir_data[7:0],lab4_tb.IR_RX.ir_data[15:8],lab4_tb.IR_RX.ir_data[23:16],lab4_tb.IR_RX.ir_data[31:24]);
  if(lab4_tb.IR_RX.repeat_ok)
    $display("time=%3d IR repeat code = %x.", $time,lab4_tb.IR_RX.repeat_ok);  
  end
    

assign ir_tx_pos =  ir_tx_d1 & ~ir_tx_d2;    
always@(posedge clk_50M)
  begin
  ir_tx_d1 <= ir_tx;
  ir_tx_d2 <= ir_tx_d1;
  end 
 
always@(posedge clk_50M)
  begin
  if(ir_tx_pos) 
    begin
     ir_tx_enlop_cnt <= 0;
     ir_tx_enlop <= 1;
    end
  else 
    begin
    if(ir_tx_enlop_cnt < 1500)  // 20ns*1500 =  30us timeout
      ir_tx_enlop_cnt <= ir_tx_enlop_cnt + 1;
    else
      ir_tx_enlop <= 0; 
    end
  end

assign ir_tx_enlop_rev = !ir_tx_enlop;
  
//assign ir_tx = ir_en ? clk_38K : 1'b0;

 
initial
  begin
  //$dumpfile("lab4_tb.vcd");  
  //$dumpvars;
      
  reset_n = 0;  
  clk_50M = 0 ;
  clk_38K = 0;
  ir_en = 0;
  ir_tx_enlop = 0;
  ir_tx_enlop_cnt = 0;
  //force key_scan = 4'h0;
  k0_ctrl = 0;
  k1_ctrl = 0;
  k2_ctrl = 0;
  k3_ctrl = 0;
  k4_ctrl = 0;
  k5_ctrl = 0;
  k6_ctrl = 0;
  k7_ctrl = 0;
  k8_ctrl = 0;
  k9_ctrl = 0;
  k10_ctrl = 0;
  k11_ctrl = 0;
  k12_ctrl = 0;
  k13_ctrl = 0;
  k14_ctrl = 0;
  k15_ctrl = 0;  
  
  #30 reset_n = 1;
 ////////////////////////
  #1_000_000;
  k0_ctrl = 1;   // press KEY 0
  #10_000_000;   // 10ms  
  k0_ctrl = 0;   // release KEY 0
 //////////////////////// 
  #1_000_000;
  k6_ctrl = 1;   // press KEY 6
  #20_000_000;   // 20ms
  k6_ctrl = 0;   // release KEY 6  
 ////////////////////////
  wait(lab4_tb.IR_RX.state == 2'b11); // wait IR TX send end
  //////////////////////
  //#3_000_000;
  #150_000_000;  // 150ms
  k9_ctrl = 1;   // press KEY 9
  #150_000_000;  // 150ms  
  k9_ctrl = 0;   // release KEY 9 
  /////////////////////
  #1_000_000;
  
  $finish;
  end
  
  
task send_ir_leader;  
 begin
  ir_en = 1;
  repeat(342)@(posedge clk_38K); // 9ms  , 342*26.315us = 9ms 
  ir_en = 0;
  repeat(171)@(posedge clk_38K); // 
  end
endtask 

task send_ir_repeat;  
 begin
  ir_en = 1;
  repeat(342)@(posedge clk_38K); // 9ms  , 342*26.315us = 9ms 
  
  ir_en = 0;
  repeat(85)@(posedge clk_38K); // 2.25ms
  
  ir_en = 1;
  repeat(DATA_HI_PERIOD)@(posedge clk_38K); // 560us
  ir_en = 0; 
  end
endtask 

task send_ir_byte;
  input [7:0] byte;
  integer i;
  begin
  $display("send ir = %x ",byte);
  for (i=0; i<8; i=i+1) 
     begin 
     ir_en = 1;
     repeat(DATA_HI_PERIOD)@(posedge clk_38K); //
     ir_en = 0;
     if(byte[0])
       repeat(DATA1_LO_PERIOD)@(posedge clk_38K); //
     else
       repeat(DATA0_LO_PERIOD)@(posedge clk_38K); // 
     byte = byte >> 1;       
     end      
  end   
endtask 

task send_ir_end;  
 begin
  ir_en = 1;
  repeat(DATA_HI_PERIOD)@(posedge clk_38K); //
  ir_en = 0;
 end
endtask
  
endmodule
```

## Wave

![lab4%20e9655fa069ff473895910c2fdbb6d0c5/Wave.png](lab4%20e9655fa069ff473895910c2fdbb6d0c5/Wave.png)

![lab4%20e9655fa069ff473895910c2fdbb6d0c5/68B630CF(key6).png](lab4%20e9655fa069ff473895910c2fdbb6d0c5/68B630CF(key6).png)

![lab4%20e9655fa069ff473895910c2fdbb6d0c5/68B6609F(key9).png](lab4%20e9655fa069ff473895910c2fdbb6d0c5/68B6609F(key9).png)

## Monitor

![lab4%20e9655fa069ff473895910c2fdbb6d0c5/monitor.png](lab4%20e9655fa069ff473895910c2fdbb6d0c5/monitor.png)