# IR_TX

```verilog
module IR_TX(
		clkSys,    //input  clkSys
		en,        //input  en
		rst_n,     //input  rst_n
		data_in,   //input  data_in
		iRDA_TX,   //output iRDA_TX
		sentfinish //output sentfinish
	);
	/*---------parameter Declarations---------*/
	//===== state machine =====//
	localparam IDLE     = 3'd0;
	localparam LEADER   = 3'd1;
	localparam DATALOAD = 3'd2;
	localparam DATASENT = 3'd3;
	localparam MAKETICK = 3'd4;
	localparam WAIT     = 3'd5;
	localparam FINISH   = 3'd6;
	localparam REPEAT   = 3'd7;
	//===== during time =====//
	//cnt_38k
	localparam durIRCLOCK  = 10'd658;    //38KHZ
	//cnt_repeat
	localparam durRepeat   = 23'd5500000;//110ms
	localparam RepeatHIGH  = 23'd450000; //9ms
	localparam RepeatLOW   = 23'd112500; //2.25ms
	localparam dur11250u   = 23'd562500; //11.25ms
	localparam dur13500u   = 23'd675000; //13.5ms
	//counter
	localparam durLeader   = 20'd675000; //13.5ms
	localparam LeaderLOW   = 20'd225000; //4.5ms
	localparam LeaderHIGH  = 20'd450000; //9ms
	localparam Data2250us  = 20'd112500; //2.25ms
	localparam Data1120us  = 20'd56000;  //1.12ms
	localparam Data1690us  = 20'd84500;  //3.932ms
	localparam Data560us   = 20'd28000;  //560us
	/*---------Ports Declarations---------*/
	input        clkSys;
	input        en;
	input        rst_n;
	input  [31:0]data_in;
	output       iRDA_TX;
	output       sentfinish;
	reg          sentfinish;
	wire         iRDA_TX;
	/*---------variables---------*/
	reg     [2:0]fstate;
	reg     [9:0]cnt_38k;
	reg    [22:0]cnt_repeat;
	reg    [19:0]counter;
	reg    [31:0]dataReg;
	reg     [4:0]index;
	reg          iRDAreg;
	reg          clk_38K;
	reg          counterEN;
	reg          cntrepeatEN;
	reg          LDEN;
	wire   [19:0]counterTemp;
	wire   [31:0]mappingReg;
	/*---------assign wire---------*/
	assign iRDA_TX     = (iRDAreg)?(clk_38K):(1'b0);
	assign counterTemp = (dataReg[31])?(Data2250us):(Data1120us);
	assign mappingReg  = {data_in[7:0],data_in[15:8],data_in[23:16],data_in[31:24]};
	/*---------counter---------*/
	always@(posedge clkSys or negedge rst_n)begin
		if(!rst_n)        counter <= 19'd0;
		else if(counterEN)counter <= counter + 19'd1;
		else              counter <= 19'd0;
	end
	/*---------cnt_38k---------*/
	always@(posedge clkSys or negedge rst_n)begin
		if(!rst_n)                   cnt_38k <= 10'd0;
		else if(cnt_38k < durIRCLOCK)cnt_38k <= cnt_38k + 10'd1;
		else                         cnt_38k <= 10'd0;
	end
	/*---------cnt_repeat---------*/
	always@(posedge clkSys or negedge rst_n)begin
		if(!rst_n)cnt_repeat <= 23'd0;
		else begin
			if(cntrepeatEN)begin
				if(cnt_repeat < durRepeat)cnt_repeat <= cnt_repeat + 23'd1;
				else                      cnt_repeat <= 23'd0;
			end
			else cnt_repeat <= 23'd0;
		end                        
	end
	/*---------clk_38K---------*/
	always@(posedge clkSys or negedge rst_n)begin
		if(!rst_n)                    clk_38K <= 1'b0;
		else if(cnt_38k == durIRCLOCK)clk_38K <= ~clk_38K;
		else                          clk_38K <= clk_38K;
	end
	/*---------fstate_state---------*/
	always@(posedge clkSys or negedge rst_n)begin
		if(!rst_n)fstate <= IDLE;
		else begin
			case(fstate)
				IDLE:begin
					if(en)fstate <= LEADER;
					else  fstate <= IDLE;
				end
				LEADER:begin
					if(counter == durLeader)fstate <= DATALOAD;
					else                    fstate <= LEADER;
				end
				DATALOAD:begin
					fstate <= DATASENT;
				end
				DATASENT:begin
					if(index == 5'd31 && counter == counterTemp)fstate <= MAKETICK;
					else                                        fstate <= DATASENT;
				end
				MAKETICK:begin
					if(counter == Data560us)fstate <= WAIT;
					else                    fstate <= MAKETICK;
				end
				WAIT:begin
					if(cnt_repeat == durRepeat)begin
						if(en)fstate <= REPEAT;
						else  fstate <= FINISH;
					end
					else    fstate <= WAIT;
				end
				FINISH:begin
					fstate <= IDLE;
				end
				REPEAT:begin
					if(cnt_repeat == durRepeat && !en)fstate <= FINISH;
					else                              fstate <= REPEAT;
				end
				default: fstate <= IDLE;
			endcase
		end
	end
	/*---------fstate_output---------*/
	always@(posedge clkSys or negedge rst_n)begin
		if(!rst_n)begin
			counterEN   <= 1'b0;
			cntrepeatEN <= 1'b0;
			iRDAreg     <= 1'b0;
			LDEN        <= 1'b0;
			index       <= 5'd0;
			sentfinish  <= 1'b0;
		end 
		else begin
			case(fstate)
				IDLE:begin
					counterEN   <= 1'b0;
					cntrepeatEN <= 1'b0;
					iRDAreg     <= 1'b0;
					LDEN        <= 1'b0;
					index       <= 5'd0;
					sentfinish  <= 1'b0;
				end
				LEADER:begin
					if(counter < durLeader) counterEN <= 1'b1;
					else                    counterEN <= 1'b0;
					if(counter < LeaderHIGH)iRDAreg   <= 1'b1;
					else                    iRDAreg   <= 1'b0;
					cntrepeatEN <= 1'b1;
					LDEN        <= 1'b0;
					index       <= 5'd0;
					sentfinish  <= 1'b0;
				end
				DATALOAD:begin
					counterEN   <= 1'b0;
					cntrepeatEN <= 1'b1;
					iRDAreg     <= 1'b0;
					index       <= 5'd0;
					LDEN        <= 1'b1;
					sentfinish  <= 1'b0;
				end
				DATASENT:begin//counterEN, iRDAreg
					if(dataReg[index])begin//Bit HIGH
						if(counter < (Data2250us-10'd1))counterEN <= 1'b1;//0-112500 
						else if(counter == Data2250us)index <= index + 5'd1;
						else counterEN <= 1'b0;
						if(counter < Data560us)iRDAreg   <= 1'b1;
						else                   iRDAreg   <= 1'b0;
					end
					else begin//Bit LOW
						if(counter < (Data1120us-10'd1))counterEN <= 1'b1;//0-56000
						else if(counter == Data1120us)index <= index + 5'd1;
						else counterEN <= 1'b0;
						if(counter < Data560us)iRDAreg   <= 1'b1;
						else                   iRDAreg   <= 1'b0;
					end
					cntrepeatEN <= 1'b1;
					LDEN        <= 1'b0;
					sentfinish  <= 1'b0;
				end
				MAKETICK:begin
					if(counter < Data560us)begin
						counterEN <= 1'b1;
						iRDAreg   <= 1'b1;
					end 
					else begin
						counterEN <= 1'b0;
						iRDAreg   <= 1'b0;
					end 
					cntrepeatEN <= 1'b1;
					index       <= 5'd0;
					LDEN        <= 1'b0;
					sentfinish  <= 1'b0;
				end
				WAIT:begin
					counterEN <= 1'b0;
					iRDAreg   <= 1'b0; 
					cntrepeatEN <= 1'b1;
					index       <= 5'd0;
					LDEN        <= 1'b0;
					sentfinish  <= 1'b0;
				end
				FINISH:begin
					counterEN   <= 1'b0;
					cntrepeatEN <= 1'b0;
					iRDAreg     <= 1'b0;
					index       <= 5'd0;
					LDEN        <= 1'b0;
					sentfinish  <= 1'b1;
				end
				REPEAT:begin//counterEN, iRDAreg
					if((cnt_repeat < RepeatHIGH )|| (cnt_repeat > dur11250u && cnt_repeat < dur13500u))iRDAreg <= 1'b1;
					else                                                                               iRDAreg <= 1'b0;
					counterEN   <= 1'b0;
					cntrepeatEN <= 1'b1;
					LDEN        <= 1'b0;
					index       <= 5'd0;
					sentfinish  <= 1'b0;
				end
				default:begin
					counterEN   <= 1'b0;
					cntrepeatEN <= 1'b0;
					LDEN        <= 1'b0;
					index       <= 5'd0;
					iRDAreg     <= 1'b0;
					sentfinish  <= 1'b0;
				end
			endcase
		end
	end
	/*---------Load Data---------*/
	always@(posedge clkSys or negedge rst_n)begin
		if(!rst_n)   dataReg <= 32'd0;
		else if(LDEN)dataReg <= mappingReg;
		else         dataReg <= dataReg;
	end
	endmodule
```

## TestBench

```verilog
`timescale 1ns/1ns
	module tb_IR_TX();
	
	parameter DATA_LO_PERIOD   = 560_000; // 560us
	parameter DATA0_HI_PERIOD  = 560_000; // 560us
	parameter DATA1_HI_PERIOD  = 1690_000; // 560us
	
	reg        clkSys;
	reg        en;
	reg        rst_n;
	reg  [31:0]data_in;
	wire       iRDA_TX;
	wire       sentfinish;
	///////////
	reg clk_38K;
	reg ir;
	///////////
	
	IR_TX U0(
		.clkSys(clkSys),    
		.en(en),        
		.rst_n(rst_n),     
		.data_in(data_in),   
		.iRDA_TX(iRDA_TX),   
		.sentfinish(sentfinish)
	);
	always #10 clkSys = ~clkSys;
	
	always #13157 clk_38K = ~clk_38K;
	initial begin
		clk_38K = 0;
		clkSys = 0;
		rst_n  = 0;
		ir = 0;
		data_in = 32'h123456a9;
		repeat(5)@(posedge clkSys)rst_n  = 1;
		en = 1;
		send_ir_leader();  
		send_ir_byte(8'h12);   // customer code
	   send_ir_byte(8'h34);   // customer code
	   send_ir_byte(8'h56);   // key code
		send_ir_byte(8'hA9);   // key code reverse
		send_ir_end();
	end
	initial begin
		repeat(100)#4000000;
		en = 0;
	end
	initial begin
		
	end
	 
	 
	task send_ir_leader;  
	 begin
	  ir = 0;
	  #9_000_000; // 9ms Lo
	  ir = 1;
	  #4_500_000; // 4.5ms Hi
	 end
	endtask 

	task send_ir_byte;
	  input [7:0] byte;
	  integer i;
	  begin
	  $display("send ir = %x ",byte);
	  for (i=0; i<8; i=i+1) 
		  begin 
		  ir = 0;
		  #(DATA_LO_PERIOD);
		  ir = 1;
		  if(byte[0])
			 #(DATA1_HI_PERIOD);
		  else
			 #(DATA0_HI_PERIOD); 
		  byte = byte >> 1;       
		  end      
	  end   
	endtask 

	task send_ir_end;  
	 begin
	  ir = 0;
	  #(DATA_LO_PERIOD);  
	  ir = 1;  
	 end
	endtask
	endmodule
```

## Wave

![IR_TX%20f1e6ab833555499088d03150307de8bf/Untitled.png](IR_TX%20f1e6ab833555499088d03150307de8bf/Untitled.png)

![IR_TX%20f1e6ab833555499088d03150307de8bf/Untitled%201.png](IR_TX%20f1e6ab833555499088d03150307de8bf/Untitled%201.png)

![IR_TX%20f1e6ab833555499088d03150307de8bf/Untitled%202.png](IR_TX%20f1e6ab833555499088d03150307de8bf/Untitled%202.png)

## Compilation Report

![IR_TX%20f1e6ab833555499088d03150307de8bf/Untitled%203.png](IR_TX%20f1e6ab833555499088d03150307de8bf/Untitled%203.png)

## RTL Viewer

![IR_TX%20f1e6ab833555499088d03150307de8bf/Untitled%204.png](IR_TX%20f1e6ab833555499088d03150307de8bf/Untitled%204.png)

## State Machine

![IR_TX%20f1e6ab833555499088d03150307de8bf/Untitled%205.png](IR_TX%20f1e6ab833555499088d03150307de8bf/Untitled%205.png)